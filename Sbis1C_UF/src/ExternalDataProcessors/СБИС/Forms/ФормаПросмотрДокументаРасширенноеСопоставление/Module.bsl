
&НаКлиенте
Перем МестныйКэш, Кэш Экспорт;
Перем МассивЗакладок;

&НаКлиенте
Перем СтрокаТЧДоИзменения, СтрокаТЧУстановить;

#Область include_local_ПолучитьМодульОбъекта

&НаСервере
Функция МодульОбъектаСервер()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаСервереБезКонтекста
Функция МодульОбъектаСерверБезКонтекста()
	Возврат Новый (Тип("ВнешняяОбработкаОбъект.СБИС"));
КонецФункции

&НаКлиенте
Функция МодульОбъектаКлиент() Экспорт
	Возврат ВладелецФормы.Кэш.СБИС.МодульОбъектаКлиент;
КонецФункции

#КонецОбласти

#Область include_local_ФормаПросмотрДокумента

//////////////////////////////////////////////////////////////////////////////////

////////////////////// Управляемое приложение/////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ТаблицаДокументовПриАктивизацииЯчейки(Элемент)
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	сбисУстановитьHTML(Элемент.ТекущиеДанные.ТекстHTML);
	Вложение = Элемент.ТекущиеДанные.Вложение[0].Значение;
	ДокРазобран = (ДокументУчетаРазобран И ИмяРеквизитаВложений = "ВложениеУчета") или (ДокументРазобран И ИмяРеквизитаВложений = "Вложение");
	Если ДокРазобран Тогда
		Если Вложение.Свойство("Направление") Тогда 
			ЗаполнитьДокументДаннымиЭД(Вложение);
			//НГС Выводим на экран тип загружаемого документа
			Если Вложение.Свойство("СтруктураИниФайла") Тогда
				Ини = МестныйКэш.ФормаНастроек.Ини(МестныйКэш, Вложение.ИмяИни);
				ИмяТекущее = МестныйКэш.ОбщиеФункции.сбисСообщитьИмяРеквизита(МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Название",Вложение.СтруктураИниФайла, МестныйКэш));
				
				сбисЭлементФормы(ЭтаФорма, "СинонимДокумента").СписокВыбора.Очистить();
				Для Каждого ТипДокумент Из Ини.мДокумент Цикл
					Название = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Название",ТипДокумент.Значение,МестныйКэш);
					Имя = ТипДокумент.Ключ;
					СписокТиповДокументов.Добавить(Название, Имя);
					сбисЭлементФормы(ЭтаФорма, "СинонимДокумента").СписокВыбора.Добавить(Название);
					Если Название = ИмяТекущее Тогда
						СинонимДокумента = Название; 	
					КонецЕсли;
				КонецЦикла;
			Иначе
				ОчиститьВкладкуЗагрузка();
			КонецЕсли;
			//КНГС
			Если Вложение.Свойство("Договор1С") Тогда
				Договор1С = Вложение.Договор1С;
			иначе
				Договор1С = "";
			КонецЕсли;
			Если Вложение.Свойство("Дата1С") Тогда
				Дата1С = Вложение.Дата1С;
			иначе
				Дата1С = "";
			КонецЕсли;
			Если Вложение.Свойство("Контрагент1С") Тогда
				Контрагент1С = Вложение.Контрагент1С;
			иначе
				Контрагент1С = "";
			КонецЕсли;
		ИначеЕсли Вложение.Свойство("НоменклатураКодКонтрагента") Тогда
			сбиЗаполнитьТаблицуНоменклатуры(Вложение);
		Иначе
			ТабличнаяЧасть.Очистить();
		КонецЕсли;
		Если Найти(Элемент.ТекущиеДанные.Статус, "Не найден контрагент") Тогда
			сбисЭлементФормы(ЭтаФорма,"НеНайденКонтрагент").Видимость = Истина;
		Иначе
			сбисЭлементФормы(ЭтаФорма,"НеНайденКонтрагент").Видимость = Ложь;
		КонецЕсли;
	Иначе
		ОчиститьВкладкуЗагрузка();
	КонецЕсли; 
	
	Если МеханизмРасширенногоСопоставленияЗапущен Тогда 
		ОбновитьСтатусыСопоставления();
	КонецЕсли;

	ЭтаФорма.ОбновитьОтображениеДанных();
КонецПроцедуры
&НаКлиенте
Процедура ВыборЗакладки(Элемент)
	// Процедура осуществляет переход по вкладкам формы	
	Элементы.Контент.ТекущаяСтраница = Элементы.Загрузка;
КонецПроцедуры
&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТаблицаДокументовДокументы1СНазвания" и Элемент.ТекущиеДанные.Документы1С.Количество()>0 Тогда
		Для Каждого Документ1С из Элемент.ТекущиеДанные.Документы1С Цикл
			ПоказатьЗначение(,Документ1С.Значение);
		КонецЦикла;
	ИначеЕсли Поле.Имя = "ТаблицаДокументовУдалить"  Тогда
		УдалитьВложение("");
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	// Если это не написать, то при закрытии формы с активной вкладкой Загрузка в следующий раз форма открывается с пустым Просмотром
	ЭтаФорма.Элементы.Контент.ТекущаяСтраница = ЭтаФорма.Элементы.Просмотр;
	ПолеHTMLДокумента = ""; // без этого при повторном открытии одного документа УПД двоились линии в визуализации
КонецПроцедуры
&НаКлиенте
Процедура Отклонить(Команда)
	ОтклонитьУтвердитьНажатие(Новый Структура("Имя","Отклонить"));
КонецПроцедуры
&НаКлиенте
Процедура Утвердить(Команда)
	ОтклонитьУтвердитьНажатие(Новый Структура("Имя","Утвердить"));
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьИдентификаторПриИзменении(Элемент)
	// Процедура записывает сопоставление номенклатуры	
	ТабЧасть = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");
	Если ЗначениеЗаполнено(ТабЧасть.ТекущиеДанные.Номенклатура) Тогда
		ГлавноеОкно = МестныйКэш.ГлавноеОкно;
		ДанныеНоменклатуры = Новый Структура("Название,Идентификатор,Номенклатура,Характеристика,GTIN",ТабЧасть.ТекущиеДанные.Название,ТабЧасть.ТекущиеДанные.Идентификатор,ТабЧасть.ТекущиеДанные.Номенклатура,ТабЧасть.ТекущиеДанные.Характеристика,ТабЧасть.ТекущиеДанные.GTIN);
		ПутьКонтрагента = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.ПутьКонтрагента;
		фрм = ГлавноеОкно.сбисНайтиФормуФункции("УстановитьСоответствиеНоменклатуры",МестныйКэш.ФормаРаботыСНоменклатурой,"", МестныйКэш);
		фрм.УстановитьСоответствиеНоменклатуры(МестныйКэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьКонтрагента, Вложение.СтруктураФайла), ДанныеНоменклатуры, МестныйКэш.Парам.КаталогНастроек, МестныйКэш.Ини);
		НомерПП = ТабЧасть.ТекущиеДанные.НомерПП - 1;
		ПутьТаблДок = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.ПутьТаблДок;
		ТаблДок = МестныйКэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьТаблДок, Вложение.СтруктураФайла);
		ТаблДок[НомерПП].Вставить(Вложение.НоменклатураКодКонтрагента, ТабЧасть.ТекущиеДанные.Идентификатор);
		НоменклатураСопоставлена = Истина;
		Для Каждого СтрТабл Из ТаблДок Цикл
			Если Не СтрТабл.Свойство(Вложение.НоменклатураКодКонтрагента) или Не ЗначениеЗаполнено(СтрТабл[Вложение.НоменклатураКодКонтрагента])  Тогда
				НоменклатураСопоставлена = Ложь;	
			КонецЕсли;
		КонецЦикла;
		Если НоменклатураСопоставлена Тогда
			СоставПакета.Удалить("Ошибка");
			Вложение.XMLДокумента = МестныйКэш.ОбщиеФункции.сбисПолучитьXMLФайлаИзСтруктуры(Вложение,МестныйКэш.XSLT);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Если ДанныеСобытия.Element.id = "Открыть" Тогда
		Если сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение.Свойство("ПолноеИмяФайла") Тогда // файл добавлен в пакет для отправки (вручную или во внешних функциях) 
			ПолноеИмяФайла = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение.ПолноеИмяФайла;
		Иначе
			ПолноеИмяФайла = КаталогВременныхФайлов()+ сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение.Файл.Имя;   // файл с онлайна
			МестныйКэш.Интеграция.СохранитьВложениеПоСсылкеВФайл(МестныйКэш,сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение.Файл.Ссылка,ПолноеИмяФайла);
		КонецЕсли;
		ЗапуститьПриложение(ПолноеИмяФайла);
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПодготовитьКЗагрузке(Команда)
	// Вставить содержимое обработчика.
	сбисРазобратьДокумент("");
	ЗаполнитьДанныеСторонИзВложения();
	Для каждого СтрокаТаблДок из ТаблицаДокументов Цикл
		
		Вложение = СтрокаТаблДок.Вложение[0].Значение;
		Если СтрокаТаблДок.МожемЗагрузитьВ1С <=0 Тогда					//Если на вложение нет ИНИшки для загрузки то ничего не делаем
			Продолжить             
		КонецЕсли;
		ТипДокумента = МестныйКэш.ОбщиеФункции.сбисСообщитьИмяРеквизита(МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Документ", Вложение.СтруктураИниФайла, МестныйКэш));
		Вложение.Вставить("ТипДокумента", ТипДокумента);
		фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("ЗаполнитьДаннымиСтруктурыФайлаСтруктуруДокумента","Документ_"+Вложение.ТипДокумента,"Документ_Шаблон",МестныйКэш);
		СтруктураДокОбъект = Новый Структура; // Структура с названиями полей, как у создаваемого документа 1С
		СтруктураФайла = Вложение.СтруктураФайла;
		СтруктураДанныхДокумента = Новый Структура;
		МестныйКэш.ОбщиеФункции.сбисСкопироватьСтруктуруНаКлиенте(СтруктураДанныхДокумента, Вложение.СтруктураИниФайла);    
		
		ПараметрыФормированияВходящегоКонтекста = Новый Структура("МестныйКэш, Вложение", МестныйКэш, Вложение);
		МодульОбъектаКлиент().СформироватьВходящийКонтекстСогласноНастройкам(ПараметрыФормированияВходящегоКонтекста);		
		фрм.ЗаполнитьДаннымиСтруктурыФайлаСтруктуруДокумента(МестныйКэш, Вложение.СтруктураФайла, СтруктураДанныхДокумента, СтруктураДокОбъект, Новый Массив,СоставПакета,Вложение, Новый Структура("Режим", "Подготовка"));
		сбисЗаполнитьДанныеВложения(Вложение, СтруктураДокОбъект);
	КонецЦикла;
	
	Если	МодульОбъектаКлиент().ПрименятьФункционалНовыеКонтрагенты(Новый Структура("СоставПакета", СоставПакета)) Тогда
		
		сбисЭлементФормы(ЭтаФорма, "УчетОрганизация").Доступность = Истина;
		сбисЭлементФормы(ЭтаФорма, "УчетКонтрагент").Доступность = Истина;
		сбисЭлементФормы(ЭтаФорма, "УчетГрузополучатель").Доступность = Истина;
	КонецЕсли;
	
	сбисЭлементФормы(ЭтаФорма,"ПодготовитьКЗагрузке").Видимость = Ложь;
	сбисЭлементФормы(ЭтаФорма,"ЗагрузитьНаВложении").Видимость = Истина;
	
	Если МеханизмРасширенногоСопоставленияЗапущен Тогда 
		ОперацииПодготовкиКЗагрузкеРасширенноеСопоставление();
	КонецЕсли;		
			
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовШифрованиеПриИзменении(Элемент)
	// Вставить содержимое обработчика.
	Если сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные<>Неопределено Тогда
		СоставПакета.Вложение[сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.НомерВложенияВПакете].Вставить("Шифрование", сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Шифрование);
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПереключитьТипВложений(Команда)
	// Вставить содержимое обработчика.
	Если ИмяРеквизитаВложений = "Вложение" Тогда
		Если Не СоставПакета.Свойство("ВложениеУчета") Тогда
			Сообщить("Отсутствуют вложения учета. Возможно в личном кабинете СБИС учет не ведется.");
			Возврат;
		КонецЕсли;
		ИмяРеквизитаВложений = "ВложениеУчета";
		сбисЭлементФормы(ЭтаФорма,"ПереключитьТипВложений").Заголовок = "Показать вложения ЭДО";
		Если Не ВложенияУчетаРазобраны Тогда
			ВложенияУчетаРазобраны = Истина;
			сч = 0;
			фрмСтатусы = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("НайтиДокументы1СПоИдВложенияСБИС",МестныйКэш.ФормаРаботыСоСтатусами,"",МестныйКэш);
			Для Каждого ВложениеУчета Из СоставПакета.ВложениеУчета Цикл
				СоставПакета.ВложениеУчета[сч].Вставить("Направление", СоставПакета.Направление);
				СоставПакета.ВложениеУчета[сч].Вставить("Идентификатор", ВложениеУчета.Тип+ВложениеУчета.Подтип+ВложениеУчета.ВерсияФормата+ВложениеУчета.ПодверсияФормата);
				ТекстHTML = МестныйКэш.Интеграция.ПолучитьHTMLВложения(МестныйКэш, СоставПакета.Идентификатор, ВложениеУчета);
				СоставПакета.ВложениеУчета[сч].Вставить("ТекстHTML",ТекстHTML);
				ДанныеДокумента1С = фрмСтатусы.НайтиДокументы1СПоИдВложенияСБИС(СоставПакета.Идентификатор, ВложениеУчета.Идентификатор, МестныйКэш.Ини, МестныйКэш.ГлавноеОкно.КаталогНастроек);
				СоставПакета.ВложениеУчета[сч].Вставить("Документы1С", Новый СписокЗначений);
				СоставПакета.ВложениеУчета[сч].Вставить("Документ1СПроведен");
				СоставПакета.ВложениеУчета[сч].Вставить("Отмечен", Истина);
				Если ЗначениеЗаполнено(ДанныеДокумента1С) Тогда
					СоставПакета.ВложениеУчета[сч].Документы1С=ДанныеДокумента1С.Ссылки;
					СоставПакета.ВложениеУчета[сч].Документ1СПроведен=ДанныеДокумента1С.Проведен;
				КонецЕсли;
				сч = сч+1;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ИмяРеквизитаВложений = "Вложение";
		сбисЭлементФормы(ЭтаФорма,"ПереключитьТипВложений").Заголовок = "Показать вложения СБИС";
	КонецЕсли;
	ЗаполнитьТаблицуДокументов(СоставПакета);
	сбисУстановитьВидимостьЭлементов(МестныйКэш);
	ЭтаФорма.Элементы.ТаблицаДокументов.Обновить();
	ТаблицаДокументовПриАктивизацииЯчейки(сбисЭлементФормы(ЭтаФорма, "ТаблицаДокументов"));
	ЭтаФорма.ОбновитьОтображениеДанных();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовОтмеченПриИзменении(Элемент)
	СтрокаТаблДок = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные;
	СоставПакета[ИмяРеквизитаВложений][СтрокаТаблДок.НомерВложенияВПакете].Вставить("Отмечен", СтрокаТаблДок.Отмечен);
КонецПроцедуры
&НаКлиенте
Процедура ОтметитьВсе(Команда)
	ОтметитьВсе = Не ОтметитьВсе;   
	ПутьТаблДок = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.ПутьТаблДок;
	ТаблДок = МестныйКэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьТаблДок, Вложение.СтруктураФайла);
	Для Каждого Строка Из ЭтаФорма.ТабличнаяЧасть Цикл
		Строка.Отмечен = ОтметитьВсе;   
		Если ОтметитьВсе Тогда
			ТаблДок[Строка.НомерПП-1].Удалить("НеЗагружать");
		Иначе
			ТаблДок[Строка.НомерПП-1].Вставить("НеЗагружать");
		КонецЕсли;
	КонецЦикла;    
	ЗаполнитьТаблицуДокументов(СоставПакета);
КонецПроцедуры

#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_Совместимость

&НаКлиенте
Функция сбисПолучитьФорму(СбисИмяФормы)
	Возврат ВладелецФормы.СбисПолучитьФорму(СбисИмяФормы)
КонецФункции

&НаКлиенте
Функция сбисЭлементФормы(Форма,ИмяЭлемента)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Форма.Элементы.Найти(ИмяЭлемента);
	КонецЕсли;
	Возврат Форма.ЭлементыФормы.Найти(ИмяЭлемента);
КонецФункции

&НаКлиенте
Функция сбисПолучитьСтраницу(Элемент, ИмяСтраницы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Элемент.ПодчиненныеЭлементы[ИмяСтраницы];
	КонецЕсли;
	Возврат Элемент.Страницы[ИмяСтраницы];
КонецФункции

#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_ВнешнийВызов

// Процедура открывает форму просмотра пакета документов	
&НаКлиенте
Процедура ПоказатьДокумент(ВходящийКэш, Пакет, ДополнительныеПараметры = Неопределено) Экспорт
	МестныйКэш		= ВходящийКэш;
	Кэш 			= ВходящийКэш;
	СоставПакета	= Пакет;
	
	ДокументРазобран		= Ложь;
	ДокументУчетаРазобран	= Ложь;
	ВложенияУчетаРазобраны	= Ложь;
	ОбновлятьГлавноеОкно	= Ложь;
	ЕстьВходящиеВложения	= Ложь;
	ОтметитьВсе				= Истина;
	ЭтоПустойПакет			= Ложь;
	ОткрытьПакет			= Истина;
	
	ЗаголовокПакета	= "";
	ПакетОрганизация= "";
	ПакетКонтрагент	= "";
	АбонентскийЯщик = "Определять автоматически"; 
	
	//1189641556 
	ПараметрыРаботы = Новый Структура;
	
	сбисЭлементФормы(ЭтаФорма,"ТулБар").Видимость = Истина;
	сбисЭлементФормы(ЭтаФорма,"ПодготовитьКЗагрузке").Видимость = Истина;
	сбисЭлементФормы(ЭтаФорма,"ЗагрузитьНаВложении").Видимость = Ложь;
	сбисЭлементФормы(ЭтаФорма,"ПереключитьТипВложений").Видимость =		СоставПакета.Свойство("ВложениеУчета")
																	И	СоставПакета.ВложениеУчета.Количество();
	ТаблицаДокументов.Очистить();
	сбисУстановитьHTML("<html></html>");
	ЭтаФорма.Открыть();
	
	//+++ МАИ 1183311220
	ТекущийРаздел = МестныйКэш.Разделы["р"+МестныйКэш.Текущий.Раздел];
	ТипДокумента = МестныйКэш.Текущий.ТипДок;
	Если ДополнительныеПараметры <> Неопределено Тогда
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
			Если ДополнительныеПараметры.Свойство("ТипДокумента") Тогда
				ТипДокумента = ДополнительныеПараметры.ТипДокумента;	
			КонецЕсли;	
			Если ДополнительныеПараметры.Свойство("ТекущийРаздел") Тогда
				ТекущийРаздел = ДополнительныеПараметры.ТекущийРаздел;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//--- МАИ 1183311220
	
	Если Не	СоставПакета.Вложение.Количество() Тогда
		ЭтоПустойПакет = Истина;
		Если Не СоставПакета.Свойство("ДобавочныеСтроки") Тогда
			ОткрытьПакет = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ОткрытьПакет И Не Кэш.Парам.Меркурий Тогда // alo Меркурий
		Сообщить("В пакете отсутствуют вложения.");
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоПустойПакет Тогда
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			Попытка
				//Симонов И.В. 19.07.17 +				
				сбисУстановитьТипНоменклауры(Кэш.Ини.Конфигурация,Кэш,ЭтаФорма,ЭлементыФормы);
				//Симонов И.В. 19.07.17 -
				сбисУстановитьТипХарактеристики(Кэш.Ини.Конфигурация,Кэш,ЭтаФорма,ЭлементыФормы);
			Исключение
				Ошибка = ОписаниеОшибки();
			КонецПопытки;
			ЭлементыФормы.ТабличнаяЧасть.Колонки.Характеристика.Видимость=Ложь;
		#Иначе
			Попытка
				Если Кэш.Ини.Конфигурация.Свойство("Номенклатура") Тогда
					ИмяСправочникаНоменклатуры = СокрЛП(Сред(Кэш.Ини.Конфигурация.Номенклатура.Значение, Найти(Кэш.Ини.Конфигурация.Номенклатура.Значение, ".")+1));
					ТипСправочникаНоменклатуры = "СправочникСсылка."+ИмяСправочникаНоменклатуры;
				Иначе
					ТипСправочникаНоменклатуры = "СправочникСсылка.Номенклатура";
				КонецЕсли;
				ЭтаФорма.Элементы.ТабличнаяЧастьНоменклатура.ОграничениеТипа = Новый ОписаниеТипов(ТипСправочникаНоменклатуры);
			Исключение
				Ошибка = ОписаниеОшибки();
			КонецПопытки;
			Попытка
				Если Кэш.Ини.Конфигурация.Свойство("ЕдиницаИзмерения") Тогда
					ИмяСправочникаЕдиницаИзмерения = СокрЛП(Сред(Кэш.Ини.Конфигурация.ЕдиницаИзмерения.Значение, Найти(Кэш.Ини.Конфигурация.ЕдиницаИзмерения.Значение, ".")+1));
					ТипСправочникаЕдиницаИзмерения = "СправочникСсылка."+ИмяСправочникаЕдиницаИзмерения;
				Иначе
					ТипСправочникаЕдиницаИзмерения = "СправочникСсылка.КлассификаторЕдиницИзмерения";
				КонецЕсли;  
				ЭтаФорма.Элементы.ТабличнаяЧастьЕдИзм.ОграничениеТипа = Новый ОписаниеТипов(ТипСправочникаЕдиницаИзмерения);
			Исключение
				Ошибка = ОписаниеОшибки();
			КонецПопытки;
			ЭтаФорма.Элементы.ТабличнаяЧасть.ПодчиненныеЭлементы.ТабличнаяЧастьХарактеристика.Видимость=Ложь;
			Если Кэш.Ини.Конфигурация.Свойство("ИмяОтбораХарактеристики") Тогда
				ИмяОтбораХарактеристики = СокрЛП(СтрЗаменить(Кэш.Ини.Конфигурация.ИмяОтбораХарактеристики.Значение,"'", ""));
			Иначе
				ИмяОтбораХарактеристики = "Номенклатура";
			КонецЕсли;
			ИмяРеквизитаКонтрагентаВДоговоре = "Контрагент";
			Если Кэш.Ини.Конфигурация.Свойство("Договоры_Контрагент") Тогда
				ИмяРеквизитаКонтрагентаВДоговоре = СокрЛП(Сред(Кэш.Ини.Конфигурация.Договоры_Контрагент.Значение, Найти(Кэш.Ини.Конфигурация.Договоры_Контрагент.Значение, ".")+1));	
			КонецЕсли;
			Договор1СУстановитьОтбор(ИмяРеквизитаКонтрагентаВДоговоре);
		#КонецЕсли
	КонецЕсли;
	
	фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("УстановитьВидимостьЭлементовВформеПросмотра","Раздел_"+ТекущийРаздел+"_"+ТипДокумента,"Раздел_"+ТекущийРаздел+"_Шаблон",МестныйКэш);	
	фрм.УстановитьВидимостьЭлементовВформеПросмотра(ЭтаФорма, СоставПакета, Кэш.Парам);
	фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("сбисСписокДополнительныхОпераций","Раздел_"+ТекущийРаздел+"_"+ТипДокумента,"Раздел_"+ТекущийРаздел+"_Шаблон",МестныйКэш);	
	СписокДопОпераций = фрм.сбисСписокДополнительныхОпераций(Кэш, ЭтаФорма);
	ЗаполнитьПрохождение(СоставПакета);
	
	ЭлементКонтент = Кэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "Контент");
	Если Не ЭлементКонтент = Неопределено Тогда
		Кэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "Контент").ТекущаяСтраница = Кэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭлементКонтент, "Просмотр");
	КонецЕсли;
	//ЭтаФорма.Элементы.Контент.ТекущаяСтраница = ЭтаФорма.Элементы.Контент.ПодчиненныеЭлементы.Просмотр;
	Если СоставПакета.Свойство("НашаОрганизация") Тогда
		Если СоставПакета.НашаОрганизация.Свойство("СвЮЛ") Тогда
			ПакетОрганизация = СоставПакета.НашаОрганизация.СвЮЛ.Название;
		ИначеЕсли СоставПакета.НашаОрганизация.Свойство("СвФЛ") Тогда
			ПакетОрганизация = СоставПакета.НашаОрганизация.СвФЛ.Фамилия+" "+СоставПакета.НашаОрганизация.СвФЛ.Имя+?(СоставПакета.НашаОрганизация.СвФЛ.Свойство("Отчество")," "+СоставПакета.НашаОрганизация.СвФЛ.Отчество,"");
		КонецЕсли;
	КонецЕсли;
	Если СоставПакета.Свойство("Контрагент") Тогда
		Если СоставПакета.Контрагент.Свойство("СвЮЛ") Тогда
			ПакетКонтрагент = СоставПакета.Контрагент.СвЮЛ.Название;
		ИначеЕсли СоставПакета.Контрагент.Свойство("СвФЛ") Тогда
			ПакетКонтрагент = СоставПакета.Контрагент.СвФЛ.Фамилия+" "+СоставПакета.Контрагент.СвФЛ.Имя+?(СоставПакета.Контрагент.СвФЛ.Свойство("Отчество")," "+СоставПакета.Контрагент.СвФЛ.Отчество,"");
		КонецЕсли;
	КонецЕсли;
	Если СоставПакета.Свойство("Участники") Тогда
		
		Грузополучатель = МодульОбъектаКлиент().ПолучитьГрузополучателяПакета(СоставПакета);
		Если Грузополучатель = Неопределено Тогда
		ИначеЕсли Грузополучатель.Свойство("СвЮЛ") Тогда
			ПакетГрузополучатель = Грузополучатель.СвЮЛ.Название;
		ИначеЕсли Грузополучатель.Свойство("СвФЛ") Тогда
			ПакетГрузополучатель = Грузополучатель.СвФЛ.Фамилия+" "+Грузополучатель.СвФЛ.Имя+?(Грузополучатель.СвФЛ.Свойство("Отчество")," "+Грузополучатель.СвФЛ.Отчество,"");
		КонецЕсли;
		
	Иначе 
		
		сбисЭлементФормы(ЭтаФорма, "ГруппаГрузополучатель").Видимость = Ложь;
		
	КонецЕсли;
	//1189641556
	ФункционалПоддержкиМаркировки = МодульОбъектаКлиент().ПолучитьЗначениеФичи(Новый Структура("НазваниеФичи", "ПоддержкаМаркировки"));
	ФункционалНовыеКонтрагенты = МодульОбъектаКлиент().ПрименятьФункционалНовыеКонтрагенты(Новый Структура("СоставПакета", СоставПакета));
	ФункционалПоддержкиПрослеживаемости = МодульОбъектаКлиент().ПолучитьЗначениеФичи(Новый Структура("НазваниеФичи", "ПоддержкаПрослеживаемости")); 
	ФункционалПоддержкиНовойМаркировки = МодульОбъектаКлиент().ПолучитьЗначениеФичи(Новый Структура("НазваниеФичи", "ПоддержкаНовойМаркировки"));
	
	Если  ФункционалНовыеКонтрагенты Тогда
		
		УстановитьТипыРеквизитовСторон();
		сбисЭлементФормы(ЭтаФорма, "УчетОрганизация").Доступность = Ложь;
		сбисЭлементФормы(ЭтаФорма, "УчетКонтрагент").Доступность = Ложь;
		сбисЭлементФормы(ЭтаФорма, "УчетГрузополучатель").Доступность = Ложь;
		
		сбисЭлементФормы(ЭтаФорма, "УчетОрганизация").Видимость = СоставПакета.Свойство("Направление") 
			И Не УчетОрганизация = Неопределено;
		
		сбисЭлементФормы(ЭтаФорма, "УчетКонтрагент").Видимость = СоставПакета.Свойство("Направление") 
			И Не УчетКонтрагент = Неопределено;  
			
		сбисЭлементФормы(ЭтаФорма, "ГруппаГрузополучатель").Видимость = Не Грузополучатель = Неопределено
			И Кэш.Парам.Свойство("ТипГрузополучателя")
			И Не Кэш.Парам.ТипГрузополучателя = "ГрузополучательНеВедется"; 
	Иначе 
		сбисЭлементФормы(ЭтаФорма, "УчетОрганизация").Видимость = Ложь;
		сбисЭлементФормы(ЭтаФорма, "УчетКонтрагент").Видимость = Ложь;
		сбисЭлементФормы(ЭтаФорма, "ГруппаГрузополучатель").Видимость = Ложь;
	КонецЕсли;

	Если СоставПакета.Свойство("Примечание") Тогда
		ПакетКомментарий = СоставПакета.Примечание;
	Иначе
		ПакетКомментарий = "";
	КонецЕсли;
	
	Если Не Кэш.Разделы.Свойство("Текущий") Тогда
		Кэш.Разделы.Вставить("Текущий", Новый Структура);
	КонецЕсли;
	Кэш.Разделы.Текущий.Вставить("ФормаПросмотрДокумента", Кэш.Текущий);
	сбисЭлементФормы(ЭтаФорма,"ПереключитьТипВложений").Заголовок = "Показать вложения СБИС";
	
	ИмяРеквизитаВложений = "Вложение";
	ЗаполнитьТаблицуДокументов(СоставПакета); 
	
	//1189641556 
	ПараметрыРаботы.Вставить("МаркировкаЗаполнена",Ложь); 
	УстановитьВидимостьЭлементовВкладкиМаркировка(); 
	
	ПараметрыРаботы.Вставить("ПрослеживаемостьЗаполнена",Ложь);
	УстановитьВидимостьЭлементовВкладкиПрослеживаемость();
	
	// Функционал расширенного сопоставления. Пинать Сычева
	МеханизмРасширенногоСопоставленияЗапущен = МодульОбъектаКлиент().ПолучитьЗначениеФичи("РасширенныйФункционалСопоставленияНоменклатуры");
	ДанныеНоменклатурДляРасширенногоСопоставления = Новый Структура; 
	
	Если МеханизмРасширенногоСопоставленияЗапущен Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ТабличнаяЧастьЗаписатьСопоставлениеНоменклатуры").Доступность = Ложь;
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ТабличнаяЧастьОткрытьФормуСопоставления").Доступность = Ложь;
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ТабличнаяЧастьСоздатьСопоставитьНоменклатуруВ1С").Доступность = Ложь;
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ОтметитьВсе").Доступность = Ложь; 
	КонецЕсли;
	
	//--------------------------------------

	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ЭтаФорма.Открыть();
	#Иначе
		ЭтаФорма.Элементы.ТаблицаДокументов.Обновить();
		Если ТаблицаДокументов.Количество() Тогда // alo Меркурий
			НомерСтроки = ТаблицаДокументов[0];
			Если МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
				НомерСтроки = НомерСтроки.ПолучитьИдентификатор();
			КонецЕсли;
			сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущаяСтрока = НомерСтроки;
		КонецЕсли;									// alo Меркурий
		ТаблицаДокументовПриАктивизацииЯчейки(сбисЭлементФормы(ЭтаФорма, "ТаблицаДокументов"));
		
		ЭтаФорма.ОбновитьОтображениеДанных();
	#КонецЕсли   
	
КонецПроцедуры 

#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_Прочее

//Отдаёт отмеченные вложения, либо всё если ничего не отмечено.
&НаКлиенте
Функция ПолучитьОтмеченныеВложения()
	
	Возврат ТаблицаДокументов.НайтиСтроки(Новый Структура("Отмечен", Истина));
	
КонецФункции

&НаКлиенте
Процедура сбисУстановитьHTML(сбисТекстHTML)
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ЭлементыФормы.ПолеHTMLДокумента.УстановитьТекст(сбисТекстHTML);
	#Иначе
		ПолеHTMLДокумента = сбисТекстHTML;
	#КонецЕсли
КонецПроцедуры
&НаКлиенте
Процедура СохранитьНаДискНажатие(Элемент) Экспорт
	// Процедура сохраняет текущий пакет на диск в zip-архив	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога; 
	ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим); 
	ДиалогОткрытия.Каталог = ""; 
	ДиалогОткрытия.МножественныйВыбор = Ложь; 
	ДиалогОткрытия.Заголовок = "Выберите каталог"; 
	
	Если ДиалогОткрытия.Выбрать() Тогда 
		ПутьККаталогу = ДиалогОткрытия.Каталог; 
		Если СоставПакета.Свойство("Вложение") Тогда
			ИмяZIP= СтрЗаменить(ЗаголовокПакета,":",".");
			ИмяZIP= СтрЗаменить(ИмяZIP,"\","_");
			ИмяZIP= СокрЛП(СтрЗаменить(ИмяZIP,"/","_"));
			//ЗаписьZIP =  Новый ЗаписьZipФайла(ПутьККаталогу+"\"+ИмяZip+".zip");
			СоздатьКаталог(ПутьККаталогу+"\"+ИмяZip);
			
			Для Каждого Вложение Из СоставПакета.Вложение Цикл
				Если Вложение.Свойство("XMLДокумента") и Вложение.XMLДокумента <> "" Тогда
					ТД = Новый ТекстовыйДокумент;
					ТД.УстановитьТекст(Вложение.XMLДокумента);
					Если Вложение.Свойство("Файл") Тогда
						Если ТипЗнч(Вложение.Файл)=Тип("Структура") Тогда   // в SDK 1.1 файл стал не массивом, а объектом
							ИмяФайла = Вложение.Файл.Имя;
						Иначе
							ИмяФайла = Вложение.Файл[0].Имя;
						КонецЕсли;
					ИначеЕсли Вложение.Свойство("СтруктураФайла") и Вложение.СтруктураФайла.Свойство("Файл") Тогда
						ИмяФайла = Вложение.СтруктураФайла.Файл.Имя+".xml";
					КонецЕсли;
					ТД.Записать(ПутьККаталогу+"\"+ИмяZip+"\"+ИмяФайла, "windows-1251");
					//ЗаписьZIP.Добавить(ВремКаталог+"\"+ИмяФайла);
				ИначеЕсли Вложение.Свойство("ПолноеИмяФайла") Тогда
					//ЗаписьZIP.Добавить(Вложение.ПолноеИмяФайла);
					КопироватьФайл(Вложение.ПолноеИмяФайла, ПутьККаталогу+"\"+ИмяZip+"\"+Вложение.ИмяФайла);
				ИначеЕсли Вложение.Свойство("Файл") и ТипЗнч(Вложение.Файл)=Тип("Структура") и Вложение.Файл.Свойство("Ссылка") и Вложение.Файл.Свойство("Имя") Тогда
					МестныйКэш.Интеграция.СохранитьВложениеПоСсылкеВФайл(МестныйКэш,Вложение.Файл.Ссылка,ПутьККаталогу+"\"+ИмяZip+"\"+Вложение.Файл.Имя);
				КонецЕсли;
			КонецЦикла;
			//ЗаписьZIP.Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//&НаКлиенте
//Процедура ТабличнаяЧастьПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
//	НомерПП = Элемент.ТекущиеДанные.НомерПП;
//	Номенклатура = Элемент.ТекущиеДанные.Номенклатура;
//	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) Тогда
//		ДанныеНоменклатуры = Новый Структура("Название,Идентификатор,Номенклатура",Элемент.ТекущиеДанные.Название,Элемент.ТекущиеДанные.Идентификатор,Элемент.ТекущиеДанные.Номенклатура);
//		УстановитьСоответствиеНоменклатуры(ДанныеНоменклатуры);
//	КонецЕсли;
//	Вложение.СтруктураФайла.Файл.Документ.ТаблДок.СтрТабл[НомерПП].Вставить("Номенклатура", Номенклатура);
//	ЗаполнитьТаблицуДокументов(СоставПакета);
//КонецПроцедуры
&НаКлиенте
Процедура ЗаполнитьПрохождение(СоставПакета) Экспорт
	// Процедура заполняет вкладку Прохождение	
	ТекущийРаздел = МестныйКэш.Разделы["р"+МестныйКэш.Текущий.Раздел];
	фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("ЗаполнитьПрохождение","Раздел_"+ТекущийРаздел+"_"+МестныйКэш.Текущий.ТипДок,"Раздел_"+ТекущийРаздел+"_Шаблон",МестныйКэш);	
	Если фрм<>Ложь Тогда
		СтруктураСобытий = фрм.ЗаполнитьПрохождение(СоставПакета);
		СБИССобытия.Очистить();
		Для Каждого Элемент Из СтруктураСобытий Цикл
			Если Не ЗначениеЗаполнено(Элемент.Значение) Тогда
				Продолжить;
			КонецЕсли;
			Если Элемент.Значение.Свойство("Название") Тогда
				Элемент.Значение.Название = МестныйКэш.ОбщиеФункции.ИзвлечьТекстИзHTML(Элемент.Значение.Название);
			КонецЕсли;
			НоваяСтр = СБИССобытия.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр, Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура СинонимДокументаПриИзменении(Элемент)
	// Процедура устанавливает тип документа 1С, который будет загружен	
	ТипДокумента = СписокТиповДокументов.НайтиПоЗначению(СинонимДокумента).Представление;
	СтрокаТаблДок = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные;
	СтруктураИниФайла = Ини.мДокумент[ТипДокумента];
	СтрокаТаблДок.Вложение[0].Значение.Вставить("СтруктураИниФайла", СтруктураИниФайла);
	СоставПакета.Вложение[СтрокаТаблДок.НомерВложенияВПакете].Вставить("СтруктураИниФайла", СтруктураИниФайла);
	Если СоставПакета.Свойство("Направление") Тогда // Исходящие пока не загружаем
		ЗаполнитьДокументДаннымиЭД(СоставПакета.Вложение[СтрокаТаблДок.НомерВложенияВПакете]);
	КонецЕсли;
	ЗаполнитьТаблицуДокументов(СоставПакета);
КонецПроцедуры
&НаКлиенте
Процедура СоздатьКонтрагента(Команда)
	// Процедура вызывает форму создания контрагента	
	СтрокаТаблДок = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные;
	фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("ЗаполнитьИзФайла","ФормаСозданияКонтрагента",,МестныйКэш);
	фрм.ЗаполнитьИзФайла(СтрокаТаблДок, СоставПакета, МестныйКэш);// alo загрузка из 3,03	
КонецПроцедуры
&НаКлиенте
Процедура ЗаполнитьДокумент1СВСоставеПакета(ИдентификаторВложения, Документ1С, СтруктураДокумента1С)
	// Процедура проставляет документы 1С на вложениях текущего пакета при загрузке документов
	сч = 0;
	Для Каждого Вложение Из СоставПакета[ИмяРеквизитаВложений] Цикл
		Если Вложение.Свойство("Идентификатор") и Вложение.Идентификатор = ИдентификаторВложения Тогда
			Если СоставПакета[ИмяРеквизитаВложений][сч].Документы1С.НайтиПоЗначению(Документ1С) = Неопределено Тогда
				СоставПакета[ИмяРеквизитаВложений][сч].Документы1С.Добавить(Документ1С); 
			КонецЕсли;
			сбисЗаполнитьДанныеВложения(СоставПакета[ИмяРеквизитаВложений][сч], СтруктураДокумента1С);
		КонецЕсли;
		сч = сч + 1;
	КонецЦикла;		
КонецПроцедуры
&НаКлиенте
Процедура ДобавитьВложение(Команда)
	// Процедура добавляет внешний файл в состав пакета
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	ДиалогОткрытияФайла.МножественныйВыбор = Истина;
	ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Шифровать = Ложь;
		Если (	МестныйКэш.Парам.СпособОбмена = 5 Или МестныйКэш.Парам.СпособОбмена = 7)
			И 	МестныйКэш.Парам.ШифроватьВыборочно = Истина и МестныйКэш.Свойство("КаталогЗашифрованных") и Лев(ДиалогОткрытияФайла.Каталог, СтрДлина(МестныйКэш.КаталогЗашифрованных)) = МестныйКэш.КаталогЗашифрованных Тогда
			Шифровать = Истина;	
		КонецЕсли;
		МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
		Для Каждого ИмяФайла Из МассивФайлов Цикл
			Файл = Новый Файл(ИмяФайла);
			СоставПакета.Вложение.Добавить(Новый Структура("ПолноеИмяФайла,ИмяФайла,Название,Шифрование",Файл.ПолноеИмя, Файл.Имя, Файл.Имя, ?(Шифровать, Истина, Ложь)));
		КонецЦикла;	
		ЗаполнитьТаблицуДокументов(СоставПакета);
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура УдалитьВложение(Команда)
	// Процедура удаляет вложение из пакета
	Если сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные<>Неопределено Тогда
		СоставПакета.Вложение.Удалить(сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.НомерВложенияВПакете);
		ЗаполнитьТаблицуДокументов(СоставПакета);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВкладкуЗагрузка()
	ТабличнаяЧасть.Очистить();
	сбисЭлементФормы(ЭтаФорма, "СинонимДокумента").СписокВыбора.Очистить();
	СинонимДокумента = "";
	Договор1С = "";
	Дата1С = "";
	Контрагент1С = "";	
	УстановитьОграничениеТипаРеквизитов();
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьДокументДаннымиЭД(Вложение) Экспорт
	// Заполняется вкладка Загрузка (тип документа, табличная часть) по данным из файла
	СтруктураФайла = Вложение.СтруктураФайла;
	ГлавноеОкно = МестныйКэш.ГлавноеОкно;
	Если Не Вложение.Свойство("СтруктураИниФайла") Тогда
		//сбисЭлементФормы(ЭтаФорма,"Загрузка").Видимость = Ложь;
		//Предупреждение("Отсутствует настройка для загрузки файла "+СтруктураФайла.Файл.Формат+"_"+СтрЗаменить(СтруктураФайла.Файл.ВерсияФормата,".", "_"));
		Возврат Ложь;
	КонецЕсли;
	сбисЭлементФормы(ЭтаФорма,"Загрузка").Видимость = Истина;
	КонтрагентРоль=МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Контрагент_Роль", Вложение.СтруктураИниФайла, МестныйКэш);
	
	//НГС Проверяем Контрагента только если есть такая роль
	Если КонтрагентРоль <> Неопределено Тогда
		Попытка
			ПутьКонтрагента = Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Данные;
			СтрКонтрагента = МестныйКэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьКонтрагента, СтруктураФайла);
		Исключение
			СтрКонтрагента = ?(СтруктураФайла.Файл.Документ.Свойство(КонтрагентРоль),СтруктураФайла.Файл.Документ[КонтрагентРоль], Неопределено);
		КонецПопытки;
		Если СтрКонтрагента = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		//ИНН = ?(СтрКонтрагента.Свойство("СвЮЛ"), СтрКонтрагента.СвЮЛ.ИНН, СтрКонтрагента.СвФЛ.ИНН);
	КонецЕсли;
	//КНГС
	
	ТабличнаяЧасть.Очистить();
	Если Найти(сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Статус, "Сопоставление не требуется")=0 Тогда
		
		Если Вложение.СтруктураИниФайла.Свойство("мТаблДок") Тогда
			Для Каждого Элемент Из Вложение.СтруктураИниФайла.мТаблДок Цикл
				Если Элемент.Значение.Свойство("ТаблДок") и Элемент.Значение.ТаблДок.Свойство("Данные") Тогда
					ПутьТаблДок = Элемент.Значение.ТаблДок.Данные;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		// << alo загрузка 3.03
		Если не ЗначениеЗаполнено(ПутьТаблДок) Тогда
			попытка
				ПутьТаблДок = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.ПутьТаблДок;
			исключение
			конецпопытки;
		конецесли;
		// alo загрузка 3.03 >>
		
		сч = 1;
		//фрм = ГлавноеОкно.сбисНайтиФормуФункции("НайтиНоменклатуруПоставщика",МестныйКэш.ФормаРаботыСНоменклатурой,"", МестныйКэш);
		//НашаНоменклатура = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("НашаНоменклатура", Вложение.СтруктураИниФайла, МестныйКэш);
		Если ЗначениеЗаполнено(ПутьТаблДок) Тогда
			ТаблДок = МестныйКэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьТаблДок, СтруктураФайла);
		Иначе
			ТаблДок = СтруктураФайла.Файл.Документ.ТаблДок.СтрТабл;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТаблДок) Тогда 
			// 1189546150
			УстановитьОграничениеТипаНоменклатуры(Вложение.СтруктураИниФайла);
			
			Для Каждого СтрТабл Из ТаблДок Цикл
				НоваяСтрока = ТабличнаяЧасть.Добавить();
				НоваяСтрока.НомерПП = сч;
				СтрТабл.Свойство("Название", НоваяСтрока.Название);
				Если СтрТабл.Свойство("Характеристика") Тогда
					Если ТипЗнч(СтрТабл.Характеристика) = Тип("Структура") Тогда
						СтрТабл.Характеристика.Свойство("Значение", НоваяСтрока.ХарактеристикаПоставщика);
					ИначеЕсли ТипЗнч(СтрТабл.Характеристика) = Тип("Массив") Тогда
						Для Каждого Элемент Из СтрТабл.Характеристика Цикл
							Элемент.Свойство("Значение", НоваяСтрока.ХарактеристикаПоставщика);
							Прервать;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				СтрТабл.Свойство("Код",		НоваяСтрока.Код);
				СтрТабл.Свойство("ЕдИзм",	НоваяСтрока.ЕдИзм);
				СтрТабл.Свойство("ОКЕИ",	НоваяСтрока.ОКЕИ);
				Сумма		= Неопределено;
				Количество	= Неопределено;
				Если	(	СтрТабл.Свойство("СуммаБезНал", Сумма)
						Или	СтрТабл.Свойство("Сумма", Сумма))
					И	СтрТабл.Свойство("Кол_во", Количество)Тогда	
				//Сумма = ?(СтрТабл.Свойство("СуммаБезНал"), Число(СтрТабл.СуммаБезНал), Число(СтрТабл.Сумма));
					Количество = ?(ЗначениеЗаполнено(Количество), Число(Количество), 0);
					Цена = Неопределено;
					Коэф = Неопределено;
					Если	СтрТабл.Свойство("Цена", Цена)
						И	СтрТабл.Свойство("Коэффициент", Коэф)
						И	ЗначениеЗаполнено(Коэф)
						И	Не Коэф = "1" Тогда
						Попытка
							Если	Коэф = "0" Или Коэф = "-0" Тогда
								Количество = 0;
								Цена = 0;
							ИначеЕсли Найти(СтрТабл.Коэффициент, "-") Тогда
							Иначе
								Коэф = Число(Коэф);
								Количество = Число(Количество * Коэф);
								Если ЗначениеЗаполнено(Сумма) И Не Количество = 0 Тогда
									Цена = Сумма/Количество;
								КонецЕсли;
							КонецЕсли
						Исключение
							Сообщить("Коэффициент не является числом");
						КонецПопытки;	
					КонецЕсли;
					НоваяСтрока.Количество = Формат(Количество, "ЧЦ=17; ЧДЦ=3; ЧРД=.; ЧГ=0; ЧН=0.000");
					НоваяСтрока.Цена = Формат(Цена,"ЧЦ=17; ЧДЦ=2; ЧРД=.; ЧГ=0; ЧН=0.00");
				Иначе
					СтрТабл.Свойство("Кол_во", НоваяСтрока.Количество);
					СтрТабл.Свойство("Цена", НоваяСтрока.Цена);
				КонецЕсли;
				СтрТабл.Свойство("СуммаБезНал", НоваяСтрока.СуммаБезНал);
				Если СтрТабл.Свойство("НДС") Тогда
					СтрТабл.НДС.Свойство("Сумма", НоваяСтрока.СуммаНДС);
					СтрТабл.НДС.Свойство("Ставка", НоваяСтрока.СтавкаНДС);
				КонецЕсли;
				СтрТабл.Свойство("Сумма", НоваяСтрока.Сумма);
				СтрТабл.Свойство("Идентификатор", НоваяСтрока.Идентификатор);
				СтрТабл.Свойство("КодПокупателя", НоваяСтрока.КодПокупателя);
				СтрТабл.Свойство("Номенклатура", НоваяСтрока.Номенклатура);
				СтрТабл.Свойство("ХарактеристикаНоменклатуры", НоваяСтрока.Характеристика); 
				СтрТабл.Свойство("GTIN", НоваяСтрока.GTIN);
				СтрТабл.Свойство("ЕдИзмОрг", НоваяСтрока.ЕдИзмОрг);
				СтрТабл.Свойство("Коэффициент", НоваяСтрока.Коэффициент);
				Если СтрТабл.Свойство("НеЗагружать") Тогда
					НоваяСтрока.Отмечен = Ложь;
				Иначе
					НоваяСтрока.Отмечен = Истина;
				КонецЕсли;
				сч = сч+1;
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
КонецФункции
&НаКлиенте
Функция сбиЗаполнитьТаблицуНоменклатуры(Вложение) Экспорт
	// Заполняется таблица сопоставления номенклатуры по данным из файла
	ГлавноеОкно = МестныйКэш.ГлавноеОкно;
	фрм = ГлавноеОкно.сбисНайтиФормуФункции("сбисЗаполнитьДанныеНоменклатурыНаСервере","РаботаСДокументами1С","", МестныйКэш);
	
	ТабличнаяЧасть.Очистить();
	сч = 1;
	ТаблДок = Вложение.СтруктураФайла.Файл.Документ.ТаблДок.СтрТабл;
	Для Каждого СтрТабл Из ТаблДок Цикл
		ДанныеНоменклатуры = фрм.сбисЗаполнитьДанныеНоменклатурыНаСервере(?(СтрТабл.Свойство("КодПокупателя") И ЗначениеЗаполнено(СтрТабл.КодПокупателя), СтрТабл.КодПокупателя, СтрТабл.Идентификатор));
		Если ЗначениеЗаполнено(ДанныеНоменклатуры) Тогда
			НоваяСтрока = ТабличнаяЧасть.Добавить();
			НоваяСтрока.НомерПП = сч;
			
			НоваяСтрока.Номенклатура = ДанныеНоменклатуры.Номенклатура;
			Если ЗначениеЗаполнено(ДанныеНоменклатуры.Характеристика) Тогда
				НоваяСтрока.Характеристика = ДанныеНоменклатуры.Характеристика;
			КонецЕсли;
			НоваяСтрока.Идентификатор = СтрТабл[Вложение.НоменклатураКодКонтрагента];
			НоваяСтрока.КодПокупателя = СтрТабл[Вложение.НоменклатураКодКонтрагента];
		КонецЕсли;
		сч = сч+1;
	КонецЦикла;	
КонецФункции

&НаКлиенте
Процедура ПриЗакрытии()
	Если МестныйКэш.Разделы.Свойство("Текущий") Тогда
		МестныйКэш.Разделы.Текущий.Удалить("ФормаПросмотрДокумента");
	КонецЕсли;
	// Обновляет контент на главном окне.
	ФормаГлавногоОкна = МестныйКэш.ГлавноеОкно;
	Если ФормаГлавногоОкна.Открыта() и ОбновлятьГлавноеОкно = Истина Тогда
		ФормаГлавногоОкна.ОбновитьКонтент();
	КонецЕсли;
	сбисЭлементФормы(ЭтаФорма,"НеНайденКонтрагент").Видимость = Ложь;
	
	фрм = ФормаГлавногоОкна.сбисНайтиФормуФункции("сбисПослеЗакрытияФормыПросмотра","РаботаСДокументами1С","", МестныйКэш);
	Если фрм<>Ложь Тогда
		фрм.сбисПослеЗакрытияФормыПросмотра(МестныйКэш); 
	КонецЕсли;
	
	Если ФормаГлавногоОкна.РежимЗапускаГлавногоОкна = "ПечатнаяФорма" Тогда
		ФормаГлавногоОкна.СбисСохранитьЗначения(Новый Структура("ВыполнитьВручную,Парам", Истина, МестныйКэш.Парам));
	КонецЕсли;   
	Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		ВосстановитьПоложениеЭлементов();
	КонецЕсли;
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "СтатусДокументаГИСМТ").Заголовок = "";
КонецПроцедуры                              

&НаКлиенте
Процедура сбисЗаполнитьДанныеВложения(Вложение, СтруктураДокОбъект)
	Попытка
		Если Вложение.СтруктураИниФайла.Свойство("Договор") и Вложение.СтруктураИниФайла.Договор.Свойство("Значение") Тогда
			ИмяРеквизита = Сред(Вложение.СтруктураИниФайла.Договор.Значение, Найти(Вложение.СтруктураИниФайла.Договор.Значение,".")+1);
			Если СтруктураДокОбъект.Свойство(ИмяРеквизита) Тогда
				Вложение.Вставить("Договор1С",СтруктураДокОбъект[ИмяРеквизита]);
			КонецЕсли;
		КонецЕсли;
		Если Вложение.СтруктураИниФайла.Свойство("Документ_Дата") и Вложение.СтруктураИниФайла.Документ_Дата.Свойство("Значение") Тогда
			ИмяРеквизита = Сред(Вложение.СтруктураИниФайла.Документ_Дата.Значение, Найти(Вложение.СтруктураИниФайла.Документ_Дата.Значение,".")+1);
			Если СтруктураДокОбъект.Свойство(ИмяРеквизита) Тогда
				Вложение.Вставить("Дата1С",СтруктураДокОбъект[ИмяРеквизита]);
			КонецЕсли;
		КонецЕсли;
		Если Вложение.СтруктураИниФайла.Свойство("Контрагент_Роль") и Вложение.СтруктураИниФайла.Контрагент_Роль.Свойство("Значение") Тогда
			КонтрагентРоль=МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Контрагент_Роль", Вложение.СтруктураИниФайла, МестныйКэш);
			Если Вложение.СтруктураИниФайла.мСторона.Свойство(КонтрагентРоль) Тогда
				ИмяРеквизита = Сред(Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Значение, Найти(Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Значение,".")+1);
				Если СтруктураДокОбъект.Свойство(ИмяРеквизита) Тогда
					Вложение.Вставить("Контрагент1С",СтруктураДокОбъект[ИмяРеквизита]);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура Договор1СПриИзменении(Элемент)
	// Вставить содержимое обработчика.
	Вложение = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение;
	Вложение.Вставить("Договор1С",Договор1С);
	Вложение.Вставить("РучноеИзменение",Истина);
КонецПроцедуры

&НаКлиенте
Процедура Контрагент1СПриИзменении(Элемент)
	// Вставить содержимое обработчика.
	Вложение = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение;
	Вложение.Вставить("Контрагент1С",Контрагент1С);
	Вложение.Вставить("РучноеИзменение",Истина);
КонецПроцедуры

&НаКлиенте
Процедура Дата1СПриИзменении(Элемент)
	// Вставить содержимое обработчика.
	Вложение = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.Вложение[0].Значение;
	Вложение.Вставить("Дата1С",Дата1С);
	Вложение.Вставить("РучноеИзменение",Истина);
КонецПроцедуры


&НаКлиенте
// <Описание процедуры>
//
Процедура УстановитьТипыРеквизитовСторон()
	
	ТипыСторон = МодульОбъектаКлиент().ТипыСторонСопоставления();
	
	УчетОрганизация = ТипыСторон.ДанныеОрганизации;
	УчетКонтрагент = ТипыСторон.ДанныеКонтрагента;
	УчетГрузополучатель = ТипыСторон.ДанныеГрузополучателя;
	
КонецПроцедуры // УстановитьТипыРеквизитовСторон()

// Процедура заполняет вкладку Маркировка
&НаКлиенте  
Процедура ЗаполнитьМаркировку(СоставПакета)
	
	ТекущийРаздел = МестныйКэш.Разделы["р"+МестныйКэш.Текущий.Раздел];
	
	СкладскиеПараметрыДокумента = МодульОбъектаКлиент().ПолучитьСкладскиеПараметрыДокумента(СоставПакета);     
	 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "ПричинаВывода").СписокВыбора.ЗагрузитьЗначения(СкладскиеПараметрыДокумента.ПричиныВыводаИзОборота); 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "УчастникОборота").СписокВыбора.ЗагрузитьЗначения(СкладскиеПараметрыДокумента.ПараметрыУчастниковОборота); 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ОтправитьКодыМаркировки").Видимость = СкладскиеПараметрыДокумента.ОтправитьКодыМаркировки; 
	
	Если ТекущийРаздел = "Продажа" Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ИдентификаторГосконтракта").Видимость = СкладскиеПараметрыДокумента.ПоказатьГосконтракт; 
		Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьНомерКонтракта").Видимость = СкладскиеПараметрыДокумента.ПоказатьГосконтракт;
		КонецЕсли;  
	КонецЕсли;

	ИдентификаторГосконтракта = СкладскиеПараметрыДокумента.ИдентификаторГосконтракта; 
	УчастникОборота = СкладскиеПараметрыДокумента.УчастникОборота; 
	ПричинаВывода = СкладскиеПараметрыДокумента.ПричинаВывода; 
	
	СостояниеПроверки = МодульОбъектаКлиент().ЗапроситьСостояниеПроверки(СоставПакета);  
	Если ТипЗнч(СостояниеПроверки) = Тип("Строка")
		И ЗначениеЗаполнено(СостояниеПроверки) Тогда
		РезультатПроверки = МестныйКэш.РаботаСJSON.СбисПрочитатьJSON(СостояниеПроверки); 
		ЗначениеПроверки = Неопределено;
		Если ТипЗнч(РезультатПроверки) = Тип("Структура")
			И РезультатПроверки.Свойство("StateValue",ЗначениеПроверки) Тогда 
			ОтобразитьСостояниеПроверки(ЗначениеПроверки); 
		КонецЕсли;
	КонецЕсли; 
	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ТаблицаМаркировка, "Строки").Очистить();
	
	ТаблицаФормы = МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "ТаблицаМаркировка");
	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ТаблицаФормы, "КодМаркировки").Видимость = Ложь;  
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ТаблицаФормы, "ИндикаторКодаМаркировки").Видимость = Ложь;
	
	МассивМаркируемыхПозиций = МодульОбъектаКлиент().СоставМаркируемыхПозиций(СоставПакета); //Проверяем есть ли маркированная номенклатура в документе 
	ЗаполнитьТаблицуМаркировки(МассивМаркируемыхПозиций);
    	
	ПараметрыРаботы.МаркировкаЗаполнена = Истина; //Устанавливаем значение переменной, что бы повторно не вычитывать вкладку Маркировка
	
КонецПроцедуры  
 

// Процедура - устанавливает оформление шапки таблицы маркируемых позиций 
//
// Параметры:
//  ЗначениеПроверки - Строка - результат проверки параметра документа CrptState 
//
&НаКлиенте
Процедура ОтобразитьСостояниеПроверки(ЗначениеПроверки)
	
	СписокЭлементовОформления = ПолучитьСостояниеПроверки(ЗначениеПроверки);
	
	ТаблицаФормы = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "ТаблицаМаркировка");
	
	Если МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда	
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "КоличествоКодов").КартинкаШапки = СписокЭлементовОформления.Картинка;  
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "КоличествоКодов").Подсказка = СписокЭлементовОформления.Подсказка; 
	Иначе
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаФормы, "КоличествоКодов").КартинкаШапки = СписокЭлементовОформления.Картинка;  
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаФормы, "КоличествоКодов").ПодсказкаВШапке = СписокЭлементовОформления.Подсказка; 
	КонецЕсли;
	
КонецПроцедуры 

// Функция - возвращает картинку и подсказку шапки таблицы маркируемых позиций 
//  
// Параметры:
//  ЗначениеПроверки - Строка - результат проверки параметра документа CrptState  
// 
// Возвращаемое значение:
//  Структура - результат подбора картинки и подсказки по значению проверки документа
// 
&НаКлиенте
Функция ПолучитьСостояниеПроверки(ЗначениеПроверки)
	
	СписокЭлементовОформления = Новый Структура;
	
	Если ЗначениеПроверки = "HasNotToken" Тогда	 
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаAlertОранжевый").Картинка;
		Подсказка = "Не запускалась. Проверьте подпись и подключение к сети";
	ИначеЕсли ЗначениеПроверки = "GtinError" Тогда
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаAlertКрасный").Картинка;
		Подсказка = "Есть ошибки в данных маркировки";
	ИначеЕсли ЗначениеПроверки = "StatusError" Тогда   
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаAlertКрасный").Картинка;
		Подсказка = "Ошибки в кодах";
	ИначеЕсли ЗначениеПроверки = "GtinSuccess" Тогда
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаSuccessЗеленый").Картинка;
		Подсказка = "Успешно"; 
	ИначеЕсли ЗначениеПроверки = "Success" Тогда
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаSuccessЗеленый").Картинка;
		Подсказка = "Успешно";
	ИначеЕсли ЗначениеПроверки = "NotValidContract" Тогда	
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаAlertОранжевый").Картинка;
		Подсказка = "Не запускалась. Нет договора по товарной группе";
	ИначеЕсли ЗначениеПроверки = "Processing" Тогда	 
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаAlertОранжевый").Картинка;
		Подсказка = "ГИС МТ не доступен";
	ИначеЕсли ЗначениеПроверки = "Unknown" Тогда	 
		Картинка = сбисЭлементФормы(ЭтаФорма,"КартинкаAlertОранжевый").Картинка;
		Подсказка = "Сервис проверки кодов недоступен";
	Иначе
		Картинка = Новый Картинка;
		Подсказка = "";
	КонецЕсли;
	
	СписокЭлементовОформления.Вставить("Картинка",Картинка);
	СписокЭлементовОформления.Вставить("Подсказка",Подсказка);

	Возврат СписокЭлементовОформления;

КонецФункции

// Процедура - заполняет таблицу значений ТаблицаМаркировка с подчиненными строками
//
// Параметры:
//  СписокМаркируемыхПозиций - Массив - список позиций для заполнения, полученный методом чтения документа СБИС Онлайн 
//
&НаКлиенте
Процедура ЗаполнитьТаблицуМаркировки(СписокМаркируемыхПозиций) 
	
	Если НЕ ЗначениеЗаполнено(СписокМаркируемыхПозиций) Тогда
		Возврат;
	КонецЕсли; 
	
	НомерПП = 1;
	Для Каждого СтрМасс из СписокМаркируемыхПозиций Цикл
		Если НЕ ЗначениеЗаполнено(СтрМасс.GisMarkData.nom_type) И НЕ СтрМасс.GisMarkData.is_gis_mark = "True" Тогда
			Продолжить; //Метод возвращает всю номенклатуру
		КонецЕсли; 
		НоваяСтрока = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаМаркировка, "Строки").Добавить(); 
		Если ЗначениеЗаполнено(СтрМасс.Номенклатура_Наименование) Тогда
			НоваяСтрока.Номенклатура = СтрМасс.Номенклатура_Наименование;
		Иначе
			НоваяСтрока.Номенклатура = СтрМасс.ВходящееНаименование_Наименование;
		КонецЕсли;
		НоваяСтрока.Количество = СтрМасс.Количество;  
		НоваяСтрока.НоменклатураСбис = СтрМасс.НоменклатураДокументаПК;
		Если ТипЗнч(СтрМасс) = Тип("Структура") //Если ОСУ, то базовая информация по КМ есть
			И СтрМасс.Свойство("VaData") 
			И ТипЗнч(СтрМасс.VaData) = Тип("Структура") 
			И СтрМасс.VaData.Свойство("va_count") Тогда
			НоваяСтрока.КоличествоКодов = СтрМасс.VaData.va_count; 
		ИначеЕсли ТипЗнч(СтрМасс) = Тип("Структура")
			И СтрМасс.Свойство("SerialNumberSnCount")
			И СтрМасс.Свойство("SerialNumberPack") 
			И СтрМасс.SerialNumberPack = 0 Тогда //Если есть и коробки и штуки, то приоритет отдаем коробкам
			НоваяСтрока.КоличествоКодов = СтрМасс.SerialNumberSnCount; 
			Если НЕ НоваяСтрока.КоличествоКодов = "" И Число(НоваяСтрока.КоличествоКодов) > 0 Тогда
				ПодчиненнаяСтрока = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(НоваяСтрока, "Строки").Добавить(); //Чтобы отобразить плюс
			КонецЕсли;     
		ИначеЕсли ТипЗнч(СтрМасс) = Тип("Структура")
			И СтрМасс.Свойство("SerialNumberPack") Тогда  //Количество упаковок
			НоваяСтрока.КоличествоКодов = СтрМасс.SerialNumberPack; 
			Если НЕ НоваяСтрока.КоличествоКодов = "" И Число(НоваяСтрока.КоличествоКодов) > 0 Тогда
				ПодчиненнаяСтрока = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(НоваяСтрока, "Строки").Добавить(); //Чтобы отобразить плюс 
			КонецЕсли; 
		КонецЕсли;
		НоваяСтрока.Цена = ?(НЕ ЗначениеЗаполнено(СтрМасс.Цена), СтрМасс.Себест, СтрМасс.Цена);
		НоваяСтрока.НомерПП = НомерПП;
		Если СтрМасс.Свойство("НоменклатураДокументаРасш_НДС") И ТипЗнч(СтрМасс.НоменклатураДокументаРасш_НДС) = Тип("Число") Тогда
			НоваяСтрока.СтавкаНДС = МодульОбъектаКлиент().СтавкаНДСПоПараметруМаркировки(СтрМасс.НоменклатураДокументаРасш_НДС); 
		КонецЕсли; 
		Если ЗначениеЗаполнено(СтрМасс.SerialNumberFlag) Тогда
			НоваяСтрока.ИндексКартинкиКод = 1;
		КонецЕсли;
		НомерПП = НомерПП + 1;  
	КонецЦикла;
	
КонецПроцедуры 

// Процедура устанавливает видимость элементов формы на вкладке Маркировка
&НаКлиенте
Процедура УстановитьВидимостьЭлементовВкладкиМаркировка() 
	
	СбисРасширение = Неопределено;   
	СтатусГос = Неопределено;  
	СостояниеОперации = Неопределено; 
	КодСостоянияОперации = Неопределено;
	лМаркировка = Неопределено; 
	
	ТекущийРаздел = МестныйКэш.Разделы["р"+МестныйКэш.Текущий.Раздел];    
	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"Маркировка").Видимость = Ложь;   
	
	Если НЕ СоставПакета.Свойство("Расширение", СбисРасширение) Тогда
		Возврат;
	КонецЕсли;  
	
	Если НЕ (СбисРасширение.Свойство("Маркировка", лМаркировка)
		И	лМаркировка = "Да") Тогда
		Возврат;
	КонецЕсли;
	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ТаблицаМаркировка, "Строки").Очистить();  
				
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"Маркировка").Видимость = Истина; 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ОтправитьКодыМаркировки").Видимость = Ложь; 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ЗапуститьПроверкуКодовМаркировки").Видимость = Истина; 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ПричинаВывода").Видимость = Истина; 
	Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьПричинаВывода").Видимость = Истина;
	КонецЕсли;  
		
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ИдентификаторГосконтракта").Видимость = Ложь;
	Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьНомерКонтракта").Видимость = Ложь;
	КонецЕсли;	
		
	Если НЕ ТекущийРаздел = "Продажа" Тогда  
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"УчастникОборота").Видимость = Ложь;		
		Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьУчастникОборота").Видимость = Ложь;
		КонецЕсли; 
	Иначе
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"УчастникОборота").Видимость = Истина;
		Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьУчастникОборота").Видимость = Истина;  
		КонецЕсли; 
	КонецЕсли;
	
	Если СбисРасширение.Свойство("СостояниеМарк",СтатусГос)  
		И СтатусГос.Свойство("СостояниеОперации", СостояниеОперации)
		И СтатусГос.Свойство("КодСостоянияОперации", КодСостоянияОперации)
		И НЕ КодСостоянияОперации = "0" Тогда 
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"СтатусДокументаГИСМТ").Заголовок = СостояниеОперации;
		ЦветСтатуса = ВернутьЦветСтатуса(КодСостоянияОперации); 
		Если НЕ ЦветСтатуса = Неопределено Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"СтатусДокументаГИСМТ").ЦветТекста = ЦветСтатуса; 
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"СтатусДокументаГИСМТ").Гиперссылка = Истина; 
		КонецЕсли;
		Если КодСостоянияОперации = "3" Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ЗапуститьПроверкуКодовМаркировки").Видимость = Ложь;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры   

// Процедура устанавливает видимость кнопки "Отправить коды". Если коды зарегистрированы, кнопку скрываем
//
// Параметры:
//  ЗначениеПроверки - Строка - результат проверки кодов маркировки
//
&НаКлиенте
Процедура УстановитьВидимостьОтправитьКодыМаркировки(ЗначениеПроверки)

	Если ЗначениеПроверки = "GtinSuccess" 
 		ИЛИ ЗначениеПроверки = "Success" Тогда
		сбисЭлементФормы(ЭтаФорма,"ОтправитьКодыМаркировки").Видимость = Ложь; 	 
	КонецЕсли;
	
КонецПроцедуры

// Процедура управляет положением кнопок перехода по закладкам
//
// Параметры:
//  НаименованиеЭлемента - Строка - Наименование элемента, который собираемся сдвигать
&НаКлиенте
Процедура УстановитьПоложениеЭлемента(НаименованиеЭлемента) 
	
	ПоложениеЭлементов = Новый Структура;
	НачальноеПоложение = сбисЭлементФормы(ЭтаФорма,НаименованиеЭлемента).Лево;
	ПоложениеЭлементов.Вставить(НаименованиеЭлемента,НачальноеПоложение);
	ПараметрыРаботы.Вставить("ПоложениеЭлементов",ПоложениеЭлементов);
	
	МассивВкладок = ВернутьМассивВкладок();
	МассивСоответсвий = Новый Массив;
	
	Для Каждого СтрМасс Из МассивВкладок Цикл
		КнопкаФормы = сбисЭлементФормы(ЭтаФорма,СтрМасс); 
		Если КнопкаФормы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЭлементТаблицы = Новый Структура;
		ЭлементТаблицы.Вставить("Видимость",КнопкаФормы.Видимость);
		ЭлементТаблицы.Вставить("Лево",КнопкаФормы.Лево);
		ЭлементТаблицы.Вставить("Имя",КнопкаФормы.Имя); 
		МассивСоответсвий.Добавить(ЭлементТаблицы);
	КонецЦикла; 
	
	ПараметрыСортировки = Новый Структура("ПоляСортировки", Новый Массив);
	ПараметрыСортировки.ПоляСортировки.Добавить(Новый Структура("Поле,Направление", "Лево",	"Убыв"));
	МассивСоответсвий = МестныйКэш.ОбщиеФункции.сбисОтсортироватьОбъект(МассивСоответсвий,ПараметрыСортировки, Ложь); 
	
	ТаблицаСоответствияЭлементов = МассивСтруктурВТаблицуЗначений(МассивСоответсвий);  	
	
	Для Каждого СтрТЗ Из ТаблицаСоответствияЭлементов Цикл
		Если СтрТЗ.Имя = НаименованиеЭлемента Тогда
			Прервать;
		КонецЕсли; 
		Если СтрТЗ.Видимость = Ложь Тогда
			сбисЭлементФормы(ЭтаФорма,НаименованиеЭлемента).Лево = СтрТЗ.Лево;
			сбисЭлементФормы(ЭтаФорма,"Закладка" + НаименованиеЭлемента).Лево = СтрТЗ.Лево;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры  

&НаСервере
Функция ВернутьМассивВкладок()
	Возврат МассивЗакладок;
КонецФункции

// Процедура восстанавливает положение элементов, сдвинутых в процедуре УстановитьПоложениеЭлемента() 
//   
&НаКлиенте
Процедура ВосстановитьПоложениеЭлементов() 
	
	Перем ПоложениеЭлементов;
	
	Если ПараметрыРаботы.Свойство("ПоложениеЭлементов",ПоложениеЭлементов) Тогда
		Для Каждого Элемент Из ПоложениеЭлементов Цикл 
			сбисЭлементФормы(ЭтаФорма,Элемент.Ключ).Лево = Элемент.Значение;
			сбисЭлементФормы(ЭтаФорма,"Закладка" + Элемент.Ключ).Лево = Элемент.Значение;
		КонецЦикла;
	КонецЕсли;		
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивСтруктурВТаблицуЗначений(МассивСтруктур)
	Результат = Новый ТаблицаЗначений;
	Если МассивСтруктур = Неопределено Или МассивСтруктур.Количество() = 0 Тогда
		Возврат Результат;
	Иначе 
		Образец = МассивСтруктур[0];
		Для Каждого  Стр из Образец Цикл
			Результат.Колонки.Добавить(Стр.Ключ );
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Стр Из МассивСтруктур Цикл
		СтрТ = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрТ, Стр);		
	КонецЦикла;
		
	Возврат Результат;
КонецФункции 

// Процедура получает список прослеживаемых позиций, с документа СБИС
//
// Параметры:
//  СоставПакета - Структура - обрабатываемый пакет (документ) 
//
&НаКлиенте
Процедура ЗаполнитьПрослеживаемость(СоставПакета)  
	
    СписокПараметровВызова = Новый Структура("Фильтр",ПараметрыФильтраПрослеживаемыхПозиций());   
	Попытка
		СписокПрослеживаемыхПозиций = МодульОбъектаКлиент().СоставПрослеживаемыхПозиций(СоставПакета,СписокПараметровВызова); //Проверяем есть ли прослеживаемая номенклатура в документе 
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ЗаполнитьПрослеживаемость");
	КонецПопытки;

	ЗаполнитьТаблицуПрослеживаемости(СписокПрослеживаемыхПозиций);
	ПараметрыРаботы.ПрослеживаемостьЗаполнена = Истина;
	
КонецПроцедуры  

// Функция возвращает параметры фильтра, для запроса прослеживаемых позиций
// 
// Возвращаемое значение:
//   СписокПараметров - Структура - параметры фильтра 
//
&НаКлиенте 
Функция ПараметрыФильтраПрослеживаемыхПозиций()
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("only_traceability_nom",Истина);   
	СписокПараметров.Вставить("add_rnpt_data",Истина);
	Возврат СписокПараметров; 
	
КонецФункции 

// Процедура заполняет таблицу прослеживаемости, позициями документа СБИС
//
// Параметры:
//  СписокПрослеживаемыхПозиций	 - Структура - прослеживаемые позиции документа СБИС
//
&НаКлиенте
Процедура ЗаполнитьТаблицуПрослеживаемости(СписокПрослеживаемыхПозиций) 
	
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаПрослеживаемость, "Строки").Очистить();
	
	Если НЕ ЗначениеЗаполнено(СписокПрослеживаемыхПозиций) Тогда
		Возврат;
	КонецЕсли;  
		
	ТаблицаФормы = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "ТаблицаПрослеживаемость");	 
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаФормы, "ТаблицаПрослеживаемостьИндикаторПроверки").Видимость = Ложь;	
	
	НомерПП = 1;
	Для Каждого СтрМасс из СписокПрослеживаемыхПозиций Цикл
		НомерПП = НомерПП + 1;
		
		НоваяСтрока = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаПрослеживаемость, "Строки").Добавить(); 
		
		Если СтрМасс.Номенклатура_Наименование = Неопределено Тогда
			НоваяСтрока.ТаблицаПрослеживаемостьНоменклатура = СтрМасс.ВходящееНаименование_Наименование; 
		Иначе 
			НоваяСтрока.ТаблицаПрослеживаемостьНоменклатура = СтрМасс.Номенклатура_Наименование;
		КонецЕсли;
		
		НоваяСтрока.ТаблицаПрослеживаемостьКоличество = СтрМасс.Количество;  
		НоваяСтрока.ТаблицаПрослеживаемостьКоличествоКодов = СтрМасс.Количество; 
		
		Если НЕ ЗначениеЗаполнено(СтрМасс.RnptData) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрВложМасс из СтрМасс.RnptData Цикл
			ПодчиненнаяСтрока = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(НоваяСтрока, "Строки").Добавить();
			ПодчиненнаяСтрока.ТаблицаПрослеживаемостьКодРНПТ = СтрВложМасс.Rnpt; 
			ПодчиненнаяСтрока.ТаблицаПрослеживаемостьКоличество = СтрВложМасс.IncQty; 
			ПодчиненнаяСтрока.ТаблицаПрослеживаемостьКоличествоКодов = СтрВложМасс.RnptQty; 
			РезультатПроверки = Неопределено;
			КодОшибки = Неопределено;
			Если СтрВложМасс.Свойство("Check", РезультатПроверки)
				И (РезультатПроверки = Неопределено
				ИЛИ (РезультатПроверки.Свойство("КодОшибки",КодОшибки)
					И (КодОшибки = Неопределено ИЛИ КодОшибки = 0))) Тогда
					Продолжить;
			КонецЕсли;
			ПодчиненнаяСтрока.ТаблицаПрослеживаемостьИндикаторПроверки = ИндикаторПроверкиПоКодуОшибки(КодОшибки); 
			МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаФормы, "ТаблицаПрослеживаемостьИндикаторПроверки").Видимость = Истина;
			НоваяСтрока.ТаблицаПрослеживаемостьИндексКартинкиКод = 1; 
			ПодчиненнаяСтрока.ТаблицаПрослеживаемостьИндексКартинкиКод = 1; 
		КонецЦикла;
			  
	КонецЦикла;
	
КонецПроцедуры  

// Функция возвращает расшифровку ошибки проверки прослеживаемых позиций
//
// Параметры:
//  КодОшибки - Число - числовое код ошибки
// 
// Возвращаемое значение:
//  Строка - расшифровка ошибки по её коду
//
&НаКлиенте
Функция ИндикаторПроверкиПоКодуОшибки(КодОшибки)  
	
	Если КодОшибки = 1 Тогда
		ИндикаторПроверки = "РНПТ не зарегистрирован";
	ИначеЕсли КодОшибки = 2 Тогда 
		ИндикаторПроверки = "Количество по прослеживаемости превышает объем партии в обороте";  
	ИначеЕсли КодОшибки = 4 Тогда 
		ИндикаторПроверки = "ЕИ по данным каталога не совпадает с ЕИ прослеживаемости"; 
	ИначеЕсли КодОшибки = 8 Тогда 
		ИндикаторПроверки = "ТНВЭД по данным каталога не совпадает с ТНВЭД прослеживаемости";  
	ИначеЕсли КодОшибки = 32 Тогда 
		ИндикаторПроверки = "Дата регистрации документа не совпадает с датой регистрации в ФНС";  
	ИначеЕсли КодОшибки = 16 Тогда 
		ИндикаторПроверки = "Не хватает остатка для списания на партии";  
	ИначеЕсли КодОшибки = 17 Тогда 
		ИндикаторПроверки = "РНПТ отсутствует";
	Иначе 
		ИндикаторПроверки = "";
	КонецЕсли;   
	
	Возврат ИндикаторПроверки;

КонецФункции

// Процедура устанавливает ограничение типа справочников для сопоставления номенклатуры
//
// Параметры:
//  СтруктураИниФайла - Структура - ини файл, разложенный в структуру значений 
//
&НаКлиенте
Процедура УстановитьОграничениеТипаНоменклатуры(СтруктураИниФайла)  
	
	ТабЧасть = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");  
	Если МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда 
		ТабНоменклатура = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТабЧасть, "ТабличнаяЧастьНоменклатура");
	Иначе
		ТабНоменклатура = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТабЧасть, "Номенклатура").ЭлементУправления;  
	КонецЕсли; 
	
	СоставОграниченийТипов = МестныйКэш.ОбщиеФункции.ТипыСправочниковНоменклатуры(СтруктураИниФайла);
 
	ТабНоменклатура.ОграничениеТипа = Новый ОписаниеТипов(СоставОграниченийТипов);

КонецПроцедуры  

// Процедура устанавливает ограничение типа, для реквизитов формы документа
//
&НаКлиенте
Процедура УстановитьОграничениеТипаРеквизитов()
	
	ИниКонфигурация = МодульОбъектаКлиент().ИниПоПараметрам("Конфигурация"); 
	
	Если ИниКонфигурация.Свойство("Договоры") Тогда   
		СправочникДоговоры = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Договоры",ИниКонфигурация, МестныйКэш); 
		Если НЕ СправочникДоговоры = Неопределено Тогда	
			ИмяСправочника = МестныйКэш.ОбщиеФункции.сбисСообщитьИмяРеквизита(СправочникДоговоры);
			ТипСправочника = "СправочникСсылка." + ИмяСправочника;
			ОписаниеТипа = Новый ОписаниеТипов(ТипСправочника);
			 
			НайденныйРеквизит = МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "Договор1С"); 
			НайденныйРеквизит.ОграничениеТипа = ОписаниеТипа; 
			Договор1С = ОписаниеТипа.ПривестиЗначение();
			НайденныйРеквизит.ВыбиратьТип = Ложь;  
		КонецЕсли;
	КонецЕсли;
	
	Если ИниКонфигурация.Свойство("Контрагенты") Тогда
		СправочникКонтрагенты = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Контрагенты",ИниКонфигурация, МестныйКэш); 
		Если НЕ СправочникКонтрагенты = Неопределено Тогда
			ИмяСправочника = МестныйКэш.ОбщиеФункции.сбисСообщитьИмяРеквизита(СправочникКонтрагенты);
			ТипСправочника = "СправочникСсылка." + ИмяСправочника;
			ОписаниеТипа = Новый ОписаниеТипов(ТипСправочника);
			 
			НайденныйРеквизит = МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "Контрагент1С"); 
			НайденныйРеквизит.ОграничениеТипа = ОписаниеТипа; 
			Контрагент1С = ОписаниеТипа.ПривестиЗначение();
			НайденныйРеквизит.ВыбиратьТип = Ложь;   
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////
//AU Переделано для совместимости ОФ УФ//

&НаКлиенте
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, ПараметрыВыбора) Экспорт
	Кэш = ПараметрыВыбора.Кэш;
	// Обработка выбранного элемента
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДокРазобран = (ДокументУчетаРазобран И ИмяРеквизитаВложений = "ВложениеУчета") или (ДокументРазобран И ИмяРеквизитаВложений = "Вложение");
	Если Не ДокРазобран Тогда
		СбисВыполнитьРазбор(ПараметрыВыбора);
	КонецЕсли;
	ТекущийРаздел = Кэш.Разделы["р"+Кэш.Разделы.Текущий.ФормаПросмотрДокумента.Раздел];
	ИмяФункции = ВыбранныйЭлемент.Значение;
	ИмяФункцииСПараметрами = ВыбранныйЭлемент.Значение; 
	Поз = Найти(ИмяФункции, "(");
	Если Поз>0 Тогда
		ИмяФункции = Лев(ИмяФункции, Поз-1);
	Иначе
		ИмяФункцииСПараметрами = ВыбранныйЭлемент.Значение+"(Кэш,ЭтаФорма)";
	КонецЕсли;
	фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции(ИмяФункции,"Раздел_"+ТекущийРаздел+"_"+Кэш.Текущий.ТипДок,"Раздел_"+ТекущийРаздел+"_Шаблон",Кэш);	
	// << alo доп операции из инишки Сервис
	Если фрм = Ложь Тогда
		фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции(ИмяФункции,"РаботаСДокументами1С");	
	КонецЕсли;
	// alo доп операции из инишки Сервис >>
	Результат = Вычислить("фрм."+ИмяФункцииСПараметрами);
КонецПроцедуры

&НаКлиенте
Процедура СбисВыполнитьРазбор(ПараметрыРазбора)
	Кэш = ПараметрыРазбора.Кэш;
	СоставПакета = Кэш.ОбщиеФункции.РазобратьСтруктуруДокументаСбис(СоставПакета, Кэш, ИмяРеквизитаВложений);
	Если ИмяРеквизитаВложений = "Вложение" Тогда
		ДокументРазобран = Истина;
	Иначе
		ДокументУчетаРазобран = Истина;
	КонецЕсли;
	сбисУстановитьВидимостьЭлементов(Кэш);
	ЗаполнитьТаблицуДокументов(СоставПакета);
	
КонецПроцедуры
&НаКлиенте
Процедура сбисУстановитьВидимостьЭлементов(ВходящийКэш)  
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	ВидимостьЭлементов = (ДокументУчетаРазобран И ИмяРеквизитаВложений = "ВложениеУчета") или (ДокументРазобран И ИмяРеквизитаВложений = "Вложение"); 
	Если Кэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		//Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш, ЭтаФорма, "Контент.Загрузка").Видимость = ВидимостьЭлементов;
		Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш, ЭтаФорма, "ТаблицаДокументов.ТаблицаДокументовСтатус").Видимость = ВидимостьЭлементов;
		Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш, ЭтаФорма, "ТаблицаДокументов.ТаблицаДокументовСтатусКартинка").Видимость = ВидимостьЭлементов;
	Иначе
		//Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш, ЭтаФорма, "Загрузка").Видимость = ВидимостьЭлементов;
		Кэш.ОбщиеФункции.СбисПолучитьЭлементФормы(Кэш, ЭтаФорма, "ТаблицаДокументов.Статус").Видимость = ВидимостьЭлементов;
	КонецЕсли;
	сбисЭлементФормы(ЭтаФорма,"ПодготовитьКЗагрузке").Видимость = Не ВидимостьЭлементов;
	сбисЭлементФормы(ЭтаФорма,"ЗагрузитьНаВложении").Видимость = ВидимостьЭлементов;
	
	//+++ 1186694647 TODO Спилить и сделать видимым всегда - после завершения проекта по сопоставлению номенклатуры.
	//						(Нижеописанные способы хранения сопоставлений будут недоступны.)
	Если НЕ (Кэш.ФормаРаботыСНоменклатурой = "СопоставлениеНоменклатуры_Регистры"
			ИЛИ Кэш.ФормаРаботыСНоменклатурой = "СопоставлениеНоменклатуры_Справочники"
			ИЛИ Кэш.ФормаРаботыСНоменклатурой = "СопоставлениеНоменклатуры_СуммовойУчет") Тогда
		Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") И НЕ МеханизмРасширенногоСопоставленияЗапущен Тогда
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьЕдИзмОрг").Видимость = Истина;
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьКоэффициент").Видимость = Истина;
		ИначеЕсли НЕ МеханизмРасширенногоСопоставленияЗапущен Тогда
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").Колонки.Коэффициент.Видимость = Истина;
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").Колонки.ЕдИзмОрг.Видимость = Истина;
		ИначеЕсли ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") И МеханизмРасширенногоСопоставленияЗапущен Тогда 
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьЕдИзмОрг").Видимость = Ложь;
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьКоэффициент").Видимость = Ложь;
		Иначе
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").Колонки.Коэффициент.Видимость = Ложь;
			сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").Колонки.ЕдИзмОрг.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	//--- 1186694647
КонецПроцедуры

&НаКлиенте
Процедура СбисПечатьСоставаПакета(СбисПакет, ПараметрыПечати) Экспорт
	Кэш = ПараметрыПечати.Кэш;
	СписокПакетов = Новый СписокЗначений;
	СписокПакетов.Добавить(Новый Структура("СоставПакета", СбисПакет));
	СписокДляВыбора = Кэш.ОбщиеФункции.ПолучитьВложенияПакетовНаПечать(СписокПакетов, ПараметрыПечати);
	Если Не СписокДляВыбора.Количество() Тогда
		Сообщить("В пакете отсутствуют файлы, для которых возможна печать");
	КонецЕсли;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если СписокДляВыбора.ОтметитьЭлементы("Выберите файлы на печать") Тогда
			СбисПечатьСоставаПакета_ПослеВыбора(СписокДляВыбора, ПараметрыПечати);
		КонецЕсли;
	#Иначе
		СписокДляВыбора.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("СбисПечатьСоставаПакета_ПослеВыбора", ЭтаФорма, ПараметрыПечати), "Выберите файлы на печать");
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура СбисПечатьСоставаПакета_ПослеВыбора(СписокРезультат, ДополнительныеПараметры) Экспорт
	Если СписокРезультат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	Кэш = ДополнительныеПараметры.Кэш;
	ОбъектыПечати = Новый Массив;
	Для Каждого СтрокаРезультат Из СписокРезультат Цикл
		Если Не СтрокаРезультат.Пометка Тогда
			Продолжить;
		КонецЕсли;
		ФайлВложения = Кэш.ОбщиеФункции.СбисПолучитьИмяВременногоФайлаКлиент("pdf");
		РезультатЗаписи = Кэш.Интеграция.СохранитьВложениеПоСсылкеВФайл(Кэш, СтрокаРезультат.Значение.СсылкаНаPDF, ФайлВложения);
		Если РезультатЗаписи = Ложь Тогда
			Возврат;
			//СбисОшибка = Кэш.ОбщиеФункции.сбисИсключение(, сбисИмяКоманды="СбисПечатьСоставаПакета", 100, "Неизвестная ошибка системы", "Не удалось сохранить pdf для печати", Новый Структура("Ссылка", ВложениеПакета.СсылкаНаPDF)); 
		КонецЕсли;
		ОбъектПечати = Новый Структура("Тип,Путь", "Файл", ФайлВложения);
		ОбъектыПечати.Добавить(ОбъектПечати);
	КонецЦикла;
	
	Кэш.ОбщиеФункции.СбисПечатьДокументов(ОбъектыПечати, ДополнительныеПараметры);	
КонецПроцедуры

//Команды

&НаКлиенте
Процедура СбисПечать(Команда) Экспорт
	СбисПечатьСоставаПакета(СоставПакета, Новый Структура("Кэш,ВФоне", МестныйКэш, Истина));
КонецПроцедуры

&НаКлиенте
Процедура сбисРазобратьДокумент(Команда) Экспорт
	СбисВыполнитьРазбор(Новый Структура("Кэш", МестныйКэш));
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасхождениеНажатие(Элемент) Экспорт
	Перем ИниДокумента, ОшибкаФормирования;
	// Процедура формирует файл расхождения по полученному документу от контрагента и фактически поступившему/заказанному товару
	// Расхождение строится по вложению с минимальным весом
	ГлавноеОкно = МестныйКэш.ГлавноеОкно;
	СписокВесов = Новый СписокЗначений;
	ЕстьДокументы1С = Ложь;
	Для каждого СтрокаТаблДок из ТаблицаДокументов Цикл
		Если СтрокаТаблДок.Документы1С.Количество()>0 Тогда
			ЕстьДокументы1С = Истина;
			Вложение = СтрокаТаблДок.Вложение[0].Значение;
			Если Вложение.Свойство("СтруктураФайла") и Вложение.Свойство("СтруктураИниФайла") Тогда
				СтруктураФайлаКонтрагента = Вложение.СтруктураФайла;
				
				Документ1С = СтрокаТаблДок.Документы1С[0].Значение;
				ИмяДокумента = МестныйКэш.ОбщиеФункции.ПолучитьРеквизитМетаданныхОбъекта(Документ1С, "Имя");
				Если Вложение.СтруктураИниФайла.Свойство("ФайлНастроекРасхождения") Тогда
					Контекст = Новый Структура("Ини, Документ, Переменные, СтруктураФайла,СоставПакета,ИниКонфигурация", Вложение.СтруктураИниФайла, Документ1С, Новый Структура, СтруктураФайлаКонтрагента,СоставПакета,МестныйКэш.Ини.Конфигурация);
					ИмяДокумента = сбисПолучитьФорму("Документ_Шаблон").РассчитатьЗначение("ФайлНастроекРасхождения",Контекст,МестныйКэш);
					//ИмяДокумента = СтрЗаменить(Вложение.СтруктураИниФайла.ФайлНастроекРасхождения.Значение,"'","");
				КонецЕсли;
				Если НЕ МестныйКэш.ини.Свойство(ИмяДокумента) Тогда
					Продолжить;
				КонецЕсли;
				ИниДокумента = МестныйКэш.ФормаНастроек.Ини(МестныйКэш, ИмяДокумента);
				ДокументОтклонения = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("ДокументОтклонения", ИниДокумента, МестныйКэш);
				Если ЗначениеЗаполнено(ДокументОтклонения) Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
// << alo Меркурий
	Если НЕ ЕстьДокументы1С Тогда	// нет сопоставление с вложениями - проверим сопоставление с пакетом
		фрм = ГлавноеОкно.сбисНайтиФормуФункции("ДокументыПоИдПакета",МестныйКэш.ФормаРаботыСоСтатусами,"",МестныйКэш);
		если фрм <> ложь тогда
			списокДокументы1С= фрм.ДокументыПоИдПакета(СоставПакета.Идентификатор, МестныйКэш.Ини.Конфигурация);
			Если списокДокументы1С.Количество()>0 Тогда
				ЕстьДокументы1С = Истина;
				для каждого Документ1С из списокДокументы1С цикл
					ИмяДокумента = МестныйКэш.ОбщиеФункции.ПолучитьРеквизитМетаданныхОбъекта(Документ1С, "Имя");
					Если МестныйКэш.ини.Свойство(ИмяДокумента) Тогда
						ДокументОтклонения = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("ДокументОтклонения", МестныйКэш.ФормаНастроек.Ини(МестныйКэш, ИмяДокумента), МестныйКэш);
						СтруктураФайлаКонтрагента = новый структура("Файл", Новый структура("ВерсияФормата","3.01")); // пустая структура документа в формате 3.01
						СтруктураФайлаКонтрагента.Файл.Вставить("Документ", новый структура("Дата,Номер,ТаблДок",СоставПакета.Дата,СоставПакета.Номер,новый Структура));
						СтруктураФайлаКонтрагента.Файл.Документ.ТаблДок.Вставить("СтрТабл", новый Массив);
					КонецЕсли;
				конеццикла;
			конецесли;
		конецесли;
	конецесли;	// alo Меркурий >>
	Если НЕ ЕстьДокументы1С Тогда
		Сообщить("Для формирования расхождения необходимо сначала загрузить документы в 1С и внести в них необходимые изменения по фактически принятым/заказанным позициям.");
	КонецЕсли;
	Если		Не ЗначениеЗаполнено(ИниДокумента) Тогда
		Сообщить("Нет настройки " + ИмяДокумента + " для формирования расхождения.");
		Возврат;
	ИначеЕсли	НЕ ЗначениеЗаполнено(ДокументОтклонения) Тогда
		Сообщить("В настройке " + ИмяДокумента + " не задана опция ""ДокументОтклонения"" формирования документа расхождения.");
		Возврат;
	КонецЕсли;
		
	фрм = ГлавноеОкно.сбисНайтиФормуФункции("ПолучитьКонтекст_Расхождение","Файл_"+ДокументОтклонения,"Файл_Шаблон", МестныйКэш);
	ЗначениеИни = МестныйКэш.ФормаНастроек.Ини(МестныйКэш, ИмяДокумента); 
	ОписаниеРасхождения = Новый Структура("СоставПакета, Документ1С, ЗначениеИни", СоставПакета, Документ1С, ЗначениеИни); 
	ДопПараметры = Новый Структура("Кэш", МестныйКэш);
	Контекст = фрм.ПолучитьКонтекст_Расхождение(ОписаниеРасхождения, ДопПараметры);
		
	Если Не Контекст.ДокументДанные.Свойство("мФайл") Тогда
		Сообщить("Отсутствует настройка для формирования расхождения по документу "+ИмяДокумента);
		Возврат;		
	КонецЕсли;
	фрм = ГлавноеОкно.сбисНайтиФормуФункции("ПрочитатьДокумент","Документ_"+ИмяДокумента,"Документ_Шаблон", МестныйКэш);
	Если Не фрм.ПрочитатьДокумент(МестныйКэш,Контекст) Тогда
		Возврат;
	КонецЕсли;
	Если Контекст.СоставПакета.Вложение.Количество() = 0 Тогда
		Сообщить("Отсутствует настройка для формирования расхождения по документу "+ИмяДокумента);
		Возврат;	
	КонецЕсли;
	СтруктураФайлаНаша = Контекст.СоставПакета.Вложение[0].СтруктураДокумента;
	
	// Формируем расхождение
	фрм = ГлавноеОкно.сбисНайтиФормуФункции("СформироватьРасхождение","Файл_"+ДокументОтклонения,"Файл_Шаблон", МестныйКэш);
	Попытка
		ВложениеРасхождение = фрм.СформироватьРасхождение(Новый Структура("СтруктураФайлаКонтрагента, ВложениеНаше, Пакет", СтруктураФайлаКонтрагента, Контекст.СоставПакета.Вложение[0],  Контекст.СоставПакета), МестныйКэш);
	Исключение
		// ветка для тех, у кого вынесено формирование расхождения во внешние функции
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОшибкаФормирования = МестныйКэш.ОбщиеФункции.СбисИсключение(ИнформацияОбОшибке, "СформироватьРасхождениеНажатие");
	КонецПопытки;
	Если Не ОшибкаФормирования = Неопределено Тогда
		// ветка для тех, у кого вынесено формирование расхождения во внешние функции
		Попытка
			ВложениеРасхождение = фрм.СформироватьРасхождение(СтруктураФайлаКонтрагента, СтруктураФайлаНаша, МестныйКэш);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Если Не Найти(ИнформацияОбОшибке.Описание, "Слишком много фактических параметров") Тогда
				//Вызов был, перегенерируем ошибку, если свалилось
				ОшибкаФормирования = МестныйКэш.ОбщиеФункции.СбисИсключение(ИнформацияОбОшибке, "СформироватьРасхождениеНажатие");
			КонецЕсли;
			МестныйКэш.ГлавноеОкно.СбисСообщитьОбОшибке(МестныйКэш, ОшибкаФормирования);
		КонецПопытки;
	КонецЕсли;
	Если ВложениеРасхождение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ВложениеРасхождение.Свойство("ЕстьРасхождения") и ВложениеРасхождение.ЕстьРасхождения Тогда
		СоставПакета.Вставить("ЕстьРасхождения", Истина);
	КонецЕсли;
	СоставПакета.Вложение.Добавить(ВложениеРасхождение);
	ЗаполнитьТаблицуДокументов(СоставПакета);
КонецПроцедуры

&НаКлиенте
Процедура ДопОперации(Команда)
	СбисПараметрыВызова = Новый Структура("Кэш", МестныйКэш);
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ФормаДопОперации = сбисПолучитьФорму("ФормаДопОперации");
		ВыбранныйЭлемент = ФормаДопОперации.Показать(МестныйКэш, СписокДопОпераций);
		ПослеВыбораИзМеню(ВыбранныйЭлемент, СбисПараметрыВызова);
	#Иначе
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМеню", ЭтаФорма, СбисПараметрыВызова);
		ПоказатьВыборИзМеню(Оповещение, СписокДопОпераций, Элементы.ДопОперации);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонтрагентаОнлайнПоСсылке(Команда) Экспорт
	Перем сбисДополнительныеПараметры;
	#Если Не ТолстыйКлиентОбычноеПриложение Тогда
		сбисДополнительныеПараметры = Новый Структура("ФормаВладелец", ЭтаФорма);
	#КонецЕсли
	МестныйКэш.ОбщиеФункции.сбисОткрытьКонтрагентаОнлайнПоСсылке(МестныйКэш, Контрагент1С, сбисДополнительныеПараметры);
КонецПроцедуры

// Процедура открывает документ на онлайне	
&НаКлиенте
Процедура ОткрытьДокументОнлайн(Команда) Экспорт
	МестныйКэш.ГлавноеОкно.ОткрытьДокументОнлайнПоПакету(СоставПакета, МестныйКэш);
КонецПроцедуры

//Загрузка

&НаКлиенте
Процедура ДокументооборотПерейти(Команда)
	
	ВыбратьДействиеСПакетом();
	
КонецПроцедуры

//Открывает форму для определения действия с пакетом.
&НаКлиенте
Процедура ВыбратьДействиеСПакетом(ВыбранноеДействие=Неопределено)
	ПараметрыПереходов = Новый Структура("СоставПакета, Конфигурация, ЕстьАннулирование, ВыбранноеДействие", СоставПакета, МестныйКэш.Ини.Конфигурация, (Не МестныйКэш.Парам.СпособОбмена = 1));
	ФормаПереходов = МестныйКэш.ГлавноеОкно.сбисПолучитьФорму("ФормаПереходы",,,ЭтаФорма);
	Если ФормаПереходов = Ложь Тогда
		Возврат;
	КонецЕсли;
	ФормаПереходов.МестныйКэш = МестныйКэш;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		РезультатПрехода = ФормаПереходов.сбисВыбратьДействие(МестныйКэш, ПараметрыПереходов);
		сбисПослеВыбораПерехода(РезультатПрехода, Новый Структура);
	#Иначе
		ФормаПереходов.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("сбисПослеВыбораПерехода",ЭтаФорма);
		ФормаПереходов.сбисВыбратьДействие(МестныйКэш, ПараметрыПереходов);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВ1С(Команда) Экспорт
	МестныйКэш.ОбщиеФункции.РезультатДействия_Получить(МестныйКэш, Новый Структура("ПредставлениеОперации, ФормаВызова", "ЗагрузкаДокумента", "ФормаПросмотрДокумента"), Истина);
	ДокументРазобранДоЗагрузки = Истина;
	ДокРазобран = (ДокументУчетаРазобран И ИмяРеквизитаВложений = "ВложениеУчета") или (ДокументРазобран И ИмяРеквизитаВложений = "Вложение");
	Если Не ДокРазобран Тогда
		ДокументРазобранДоЗагрузки = Ложь;
		сбисРазобратьДокумент("");
		ВыборЗакладки(сбисЭлементФормы(ЭтаФорма,"Загрузка"));
	КонецЕсли;
	СоставПакета = МестныйКэш.ОбщиеФункции.ЗаполнитьРеквизитыОснованийПакета(СоставПакета, МестныйКэш); // пока SDK не возвращает дату, номер док. основания
	Если СоставПакета.НоменклатураСопоставлена Тогда //Если номенклатура сопоставлена
		ЗагрузитьВложения(0);  						 //Загружаем  в обычном режиме	
	Иначе                     						 //Иначе справшиваем хотим ли загружать автоматически 
		Если МестныйКэш.Парам.СпособЗагрузки = 1 Тогда // Мусихина: убрала форму выбора, если в настройках заранее выбрали способ загрузки 1 (создавать карточки номенклатуры для несопоставленных позиций)
			ЗагрузитьВ1СВыбраннымСпособом(1);
		ИначеЕсли ДокументРазобранДоЗагрузки Тогда // Если документ разобрали только что, то ничего не спрашиваем, сразу показываем вкладку с сопоставлением
			МассивОтмеченных = ТаблицаДокументов.НайтиСтроки(Новый Структура("Отмечен", Истина));
			Если МассивОтмеченных.Количество() = 0 Тогда
				Сообщить("Не отмечны документы для загрузки!");
				Возврат;
			КонецЕсли;
			ИниКонфигурации = МодульОбъектаКлиент().ИниПоПараметрам("Конфигурация");
			АвтоматическоеСозданиеНоменклатуры = Кэш.ОбщиеФункции.РассчитатьЗначение("АвтоматическоеСозданиеНоменклатуры", ИниКонфигурации);
			РежимыВыбора = Новый СписокЗначений();
			РежимыВыбора.Добавить(0, "Загружать только документы, где номенклатура полностью сопоставлена.");
			РежимыВыбора.Добавить(2, "Загружать все документы, при этом в табличную часть загружать только сопоставленную номенклатуру.");                             
			РежимыВыбора.Добавить(3, "Загружать все документы, загружать всю табличную часть, при этом для несопоставленных позиций не указывать номенклатуру.");
			Если 		АвтоматическоеСозданиеНоменклатуры = Истина
					Или	АвтоматическоеСозданиеНоменклатуры = Неопределено Тогда											
				РежимыВыбора.Вставить(1, 1, "Продолжить загрузку. Для несопоставленных позиций создавать карточки номенклатуры.");
			КонецЕсли;
			ОбработчикВыбора = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("ЗагрузитьВ1СВыбраннымСпособом", ЭтаФорма);
			МодульОбъектаКлиент().СбисВыбратьИзСписка(РежимыВыбора, Новый Структура("Обработчик, Заголовок", ОбработчикВыбора, "Вариант загрузки документов"));

//			#Если ТолстыйКлиентОбычноеПриложение Тогда
//				ФормаВариантЗагрузки = сбисПолучитьФорму("ФормаВариантЗагрузки");
//				сбисЭлементФормы(ФормаВариантЗагрузки,"СпособЗагрузки").Заголовок = "Отменить загрузку. Перейти к сопоставлению номенклатуры";
//				Ответ = ФормаВариантЗагрузки.ОткрытьМодально();
//				ЗагрузитьВ1СВыбраннымСпособом(Ответ);
//			#Иначе
//				ОткрытьФорму(МестныйКэш.ГлавноеОкно.СбисПутьКФормамОбработки() + "ФормаВариантЗагрузки",Новый Структура("Режим",Режим),,"ФормаВариантЗагрузки",,,);  
//			#КонецЕсли
		КонецЕсли;	
	КонецЕсли;
	ОбновлятьГлавноеОкно = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВ1СВыбраннымСпособом(Ответ, Параметры=Неопределено) Экспорт

	Если Ответ = Неопределено Тогда
		
		Возврат;
		
	ИначеЕсли ТипЗнч(Ответ) = Тип("ЭлементСпискаЗначений") Тогда
		
		ВыбранныйРежимЗагрузкиДокумента = Ответ.Значение;
		
	Иначе
		
		ВыбранныйРежимЗагрузкиДокумента = Ответ;
		
	КонецЕсли;
	

	Если ВыбранныйРежимЗагрузкиДокумента = 0 Тогда
		
		ВыборЗакладки(МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"Загрузка"));
		
	Иначе
		
		ЗагрузитьВложения(ВыбранныйРежимЗагрузкиДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВложения(РежимНоменклатуры)
		
	ОбработчикВыбора = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("ЗагрузитьВложенияСоздатьПерезаполнить", ЭтаФорма, РежимНоменклатуры);
	Для Каждого СтрокаТаблДок Из ТаблицаДокументов Цикл
		
		Если	СтрокаТаблДок.Документы1С.Количество()
			И	СтрокаТаблДок.МожемЗагрузитьВ1С
			И	СтрокаТаблДок.Отмечен Тогда
			
			Режим = Новый СписокЗначений();
			Режим.Добавить(0, "Перезаполнить");
			Режим.Добавить(1, "Создать новые");
			Режим.Добавить(2, "Отменить");
			
			МодульОбъектаКлиент().СбисПоказатьВопрос(ОбработчикВыбора, "Есть сопоставленные документы. Выберите вариант загрузки.", Режим);
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	МодульОбъектаКлиент().ВыполнитьСбисОписаниеОповещения(1, ОбработчикВыбора);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВложенияСоздатьПерезаполнить(РежимДокументов, РежимНоменклатуры) Экспорт
	
	Если ТипЗнч(РежимДокументов) = Тип("ЭлементСпискаЗначений") Тогда
		
		//Пришли из диалога
		РежимДокументов = РежимДокументов.Значение;
				
	КонецЕсли;
	
	Если	РежимДокументов = Неопределено
		Или	РежимДокументов = 2 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОбработки		= Новый Структура("Кэш", МестныйКэш);
	ПараметрыЗагрузки		= Новый Структура("РежимДокументов, РежимНоменклатуры", РежимДокументов, РежимНоменклатуры);
	ДополнительныеПараметры	= Новый Структура();
	ДополнительныеПараметры.Вставить("СтрокаПоУмолчанию", сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные);
	//Делаем для диалога в процессе загрузки: должны попасть в обработку результата даже в случае показа форм, вопросов и пр. 
	//#Если Не ТолстыйКлиентОбычноеПриложение Тогда
		ДополнительныеПараметры.Вставить("ОбработчикРезультата", МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("СозданиеДокуметов_ОбработатьРезультат", ЭтаФорма, ПараметрыОбработки));
	//#КонецЕсли
	МассивОтмеченных = ТаблицаДокументов.НайтиСтроки(Новый Структура("Отмечен", Истина));
	Если МассивОтмеченных.Количество() = 0 Тогда
		Сообщить("Не отмечны документы для загрузки!");
		Возврат;
	КонецЕсли;
		
	ПодготовленныеПараметрыКонтекста = ПараметрыДляФормированияКонтекста(МестныйКэш);

	ПараметрыФормированияКонтекста = Новый Структура;
	ПараметрыФормированияКонтекста.Вставить("СоставПакета", СоставПакета); 
	ПараметрыФормированияКонтекста.Вставить("ПодготовленныеПараметрыКонтекста", ПодготовленныеПараметрыКонтекста); 
	ПараметрыФормированияКонтекста.Вставить("Вложение", Неопределено);  
	ПараметрыФормированияКонтекста.Вставить("ЗаменятьЗаполненныеПараметры", Истина);  
	
	МодульОбъектаКлиент().СФормироватьВходящийКонтекстНаВложении(ПараметрыФормированияКонтекста);

	РезультатЗагрузки = МестныйКэш.ОбщиеФункции.СоздатьПерезаполнитьДокументы(МестныйКэш, СоставПакета, ТаблицаДокументов, ПараметрыЗагрузки, ДополнительныеПараметры);
	СозданиеДокуметов_ОбработатьРезультат(РезультатЗагрузки, ПараметрыОбработки);
	
КонецПроцедуры

&НаКлиенте
Функция  ПараметрыДляФормированияКонтекста(ВходящийКэш)
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	
	ПодготовленныеПараметры = Новый Структура;
	
	ДанныеСторонСБИС = Новый Структура("ДанныеГрузополучателя, ДанныеКонтрагента");
	ДанныеСторонСБИС.ДанныеКонтрагента		= Новый Структура("Ссылка", УчетКонтрагент);
	ДанныеСторонСБИС.ДанныеГрузополучателя	= Новый Структура("Ссылка", УчетГрузополучатель);
	
	ДанныеСторон1С = Новый Структура;                                                                             
	ПараметрыПодготовки = Новый Структура("ДанныеСторонСБИС, ДанныеСторон1С", ДанныеСторонСБИС, ДанныеСторон1С);
	МодульОбъектаКлиент().ПодготовитьСтороныКЗагррузкеДокумента(ПараметрыПодготовки, Новый Структура("ЕстьПартнеры", Кэш.ини.Конфигурация.Свойство("Партнеры")));
	
	ПодготовленныеПараметры.Вставить("Организация", 	 УчетОрганизация);
	ПодготовленныеПараметры.Вставить("Контрагент", 		 ДанныеСторон1С.ДанныеКонтрагента);
	ПодготовленныеПараметры.Вставить("Грузополучатель",  ДанныеСторон1С.ДанныеГрузополучателя);
	ПодготовленныеПараметры.Вставить("Партнер", 		 ДанныеСторон1С.ДанныеПартнера);
	
	Возврат ПодготовленныеПараметры;
	
КонецФункции 

// Процедура заполняет таблицу вложений пакета 	
&НаКлиенте
Процедура ЗаполнитьТаблицуДокументов(СоставПакета) Экспорт

	ГлавноеОкно		= МестныйКэш.ГлавноеОкно;
	ЗначениеПоиска	= ?(сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные<>Неопределено,сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.НомерВложенияВПакете, Неопределено);
	ТаблицаДокументов.Очистить();
	СоставПакета.Вставить("НоменклатураСопоставлена",Истина);
	Если СоставПакета.Свойство("ДобавочныеСтроки") Тогда
		Для Каждого ДобавочнаяСтрока Из СоставПакета.ДобавочныеСтроки Цикл
			НоваяСтр = ТаблицаДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр, ДобавочнаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
	сч = 0;
	ЕстьХарактеристики = Ложь;
	Для Каждого Вложение Из СоставПакета[ИмяРеквизитаВложений] Цикл
		НоваяСтр = ТаблицаДокументов.Добавить();
		НоваяСтр.НомерВложенияВПакете = сч;
		сч = сч+1;
		Если Вложение.Свойство("Название") Тогда
			НоваяСтр.Название = Вложение.Название;
		КонецЕсли;
		Если Вложение.Свойство("Идентификатор") Тогда
			НоваяСтр.Идентификатор = Вложение.Идентификатор;
		КонецЕсли;
		Если Вложение.Свойство("Шифрование") Тогда
			НоваяСтр.Шифрование = Вложение.Шифрование;
		КонецЕсли;
		Если Вложение.Свойство("Документы1С") Тогда
			НоваяСтр.Документы1С = Вложение.Документы1С;
			НоваяСтр.Документы1СНазвания = "";
			Для Каждого Документ1С Из Вложение.Документы1С Цикл
				НоваяСтр.Документы1СНазвания = НоваяСтр.Документы1СНазвания + строка(Документ1С.Значение)+Символы.ПС;
			КонецЦикла;
		КонецЕсли;
		Если Вложение.Свойство("Подпись") Тогда
			Для Каждого Подпись Из Вложение.Подпись Цикл
				НоваяСтр.Подпись = НоваяСтр.Подпись + МестныйКэш.ОбщиеФункции.ПолучитьПредставлениеСертификата(Подпись.Сертификат, "[ФИО]., [Должность]., [Название]") + Символы.ПС;
			КонецЦикла;
		КонецЕсли;
		Если Вложение.Свойство("ТекстHTML") и ЗначениеЗаполнено(Вложение.ТекстHTML) Тогда
			НоваяСтр.ТекстHTML = Вложение.ТекстHTML;
		Иначе
			НоваяСтр.ТекстHTML = "<HTML><BODY scroll=no><table cellspacing=0 cellpadding=0 WIDTH=100%><tr><td id=Открыть><ins id=Открыть>Открыть в другой программе</ins></td></tr></table></BODY></HTML>";
		КонецЕсли;
		НоваяСтр.Вложение.Добавить(Вложение);
				
		Если НЕ СоставПакета.Свойство("ДопПараметрыСопоставления") Тогда
			ДанныеДляЗагрузки = МестныйКэш.ОбщиеФункции.ЗаполнитьДанныеДляЗагрузкиПоУмолчанию(МестныйКэш, СоставПакета, Вложение);
		Иначе
			ДопПараметры = Новый Структура("ДанныеНоменклатурДляРасширенногоСопоставления", ДанныеНоменклатурДляРасширенногоСопоставления); 
			ДанныеДляЗагрузки = МестныйКэш.ОбщиеФункции.ЗаполнитьДанныеДляЗагрузкиПоУмолчанию(МестныйКэш, СоставПакета, Вложение, ДопПараметры);
		КонецЕсли;
		
		Если ДанныеДляЗагрузки.Свойство("ОбогащенныеДанныеНоменклатурыДляСопоставления") Тогда
			ЗаполнитьДанныеНоменклатурДляРасширенногоСопоставления(ДанныеДляЗагрузки.ОбогащенныеДанныеНоменклатурыДляСопоставления);
		КонецЕсли;
		
		НоваяСтр.МожемЗагрузитьВ1С	= Число(ДанныеДляЗагрузки.МожемЗагрузитьВ1С);
		НоваяСтр.Статус				= ДанныеДляЗагрузки.Статус;
		НоваяСтр.СтатусКартинка		= ДанныеДляЗагрузки.СтатусКартинка;
		Если Вложение.Свойство("Отмечен")  Тогда
			НоваяСтр.Отмечен = Вложение.Отмечен;
		Иначе
			НоваяСтр.Отмечен = Истина;
		КонецЕсли;
		Если ДанныеДляЗагрузки.Свойство("ТекстОшибки") Тогда
			Сообщить(ДанныеДляЗагрузки.ТекстОшибки);
		КонецЕсли;
		ДанныеДляЗагрузки.Свойство("ПутьТаблДок",		НоваяСтр.ПутьТаблДок);
		ДанныеДляЗагрузки.Свойство("ПутьКонтрагента",	НоваяСтр.ПутьКонтрагента);
		
		Если Не ЕстьХарактеристики
			И	ДанныеДляЗагрузки.Свойство("ПараметрыНоменклатуры") Тогда
			ЕстьХарактеристики = ДанныеДляЗагрузки.ПараметрыНоменклатуры.ЕстьХарактеристики;
		КонецЕсли;
	КонецЦикла;
	ЭлементФормыХарактеристика = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьХарактеристика");
	Если Не МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		ЭлементФормыХарактеристика = ЭтаФорма.ЭлементыФормы.ТабличнаяЧасть.Колонки.Характеристика;
	КонецЕсли; 
	ЭлементФормыХарактеристика.Видимость = ЕстьХарактеристики;
	
	Если ЗначениеПоиска <> Неопределено Тогда
		ПараметрыОтбора = Новый Структура("НомерВложенияВПакете", ЗначениеПоиска);
		Строка = ТаблицаДокументов.НайтиСтроки(ПараметрыОтбора);
		//Берем первую строку из массива (Т.к. поиск по документу или по ИдСБИС, то строка единственная в таблице)
		Если Строка.Количество() Тогда
			НомерСтроки = Строка[0];
			Если МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
				НомерСтроки = НомерСтроки.ПолучитьИдентификатор();
			КонецЕсли;
			сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущаяСтрока = НомерСтроки;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьУтвердитьНажатие(Элемент)
	// Процедура отклоняет/утверждает текущий пакет	
	ГлавноеОкно = МестныйКэш.ГлавноеОкно;
	Если Элемент.Имя = "Утвердить" Тогда
		ВыбранноеДействие = СоставПакета.Этап[0].Действие[0];
	Иначе
		ВыбранноеДействие = СоставПакета.Этап[0].Действие[1];
	КонецЕсли;
	
	Если	ВыбранноеДействие.ТребуетКомментария = "Да"
		Или	(	ВыбранноеДействие.Свойство("ТребуетИсполнителя")
			И	ВыбранноеДействие.ТребуетИсполнителя = "Да")	Тогда                                             //aa.uferov При выполнении действия с исполнителем, требуется ини конфигурации
		ВыбратьДействиеСПакетом(ВыбранноеДействие);
	Иначе
		сбисПослеВыбораПерехода(Новый Структура("Действие,Комментарий",ВыбранноеДействие, ""), "");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция СозданиеДокуметов_ОбработатьРезультат(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
		
	Если РезультатЗагрузки = Неопределено Тогда
		//В процессе был диалог, а значит для УФ с результатом попадём уже после выбора и последующей обработки. Для ОФ уже попадали
		Возврат Неопределено;
	КонецЕсли;
	
	//Заполняем данные формы в зависимости от результата.
	Для Каждого КлючИЗначение Из РезультатЗагрузки.Ошибки.ДетализацияОшибок Цикл
		Для Каждого СтрокаДетализации Из КлючИЗначение.Значение Цикл
			Если Найти(СтрокаДетализации.Сообщение, "Автоматическое создание номенклатуры при загрузке не поддерживается для Вашей конфигурации") Тогда
				ВыборЗакладки(сбисЭлементФормы(ЭтаФорма,"Загрузка"));
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Для Каждого КлючИЗначение Из РезультатЗагрузки.Действие.ДетализацияВыполнено Цикл
		Для Каждого СтрокаДетализации Из КлючИЗначение.Значение Цикл
			Для Каждого СтрокаРасшифровки Из СтрокаДетализации.ОбработаныОбъекты1С Цикл
				Если Не СтрокаРасшифровки.Свойство("СтруктураДокумента1С") Тогда
					Продолжить;
				КонецЕсли;
				ЗаполнитьДокумент1СВСоставеПакета(СтрокаДетализации.ИдентификаторВложения, СтрокаРасшифровки.Ссылка, СтрокаРасшифровки.СтруктураДокумента1С);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	ПараметрыСообщить = Новый Структура("СообщитьНеВыполнено", Ложь);
	МестныйКэш.ОбщиеФункции.РезультатДействия_СообщитьРезультат(РезультатЗагрузки, ПараметрыСообщить);
	
	ЗаполнитьТаблицуДокументов(СоставПакета);	
	сбисЭлементФормы(ЭтаФорма,"ПодготовитьКЗагрузке").Видимость = Ложь;
	сбисЭлементФормы(ЭтаФорма,"ЗагрузитьНаВложении").Видимость = Истина;
	
КонецФункции

&НаКлиенте
Функция сбисПослеВыбораПерехода(Результат, ДополнительныеПараметры) Экспорт
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ПараметрыЗавершенияПерехода = Новый Структура("Кэш, РезультатВыбораПерехода, СчСерт, СписокСертификатов", МестныйКэш, Результат, 0);

	Если Результат.Действие.ТребуетПодписания = "Да" Тогда
		СписокИНН = Новый СписокЗначений;
		ИННОрг = ?(СоставПакета.НашаОрганизация.Свойство("СвФЛ"),СоставПакета.НашаОрганизация.СвФЛ.ИНН,СоставПакета.НашаОрганизация.СвЮЛ.ИНН);
		СписокИНН.Добавить(ИННОрг);
		
		ПараметрыЗавершенияПерехода.СписокСертификатов = МестныйКэш.Интеграция.ПолучитьСертификатыДляАктивации(МестныйКэш, СписокИНН);
		Для Каждого Элемент Из ПараметрыЗавершенияПерехода.СписокСертификатов Цикл
			Сертификат		= Элемент.Значение;
			Если Сертификат.Ключ.СпособАктивации<>"СтатическийКод" Тогда
				МестныйКэш.Интеграция.ПолучитьКодАктивацииСертификата(МестныйКэш, Сертификат);				
			КонецЕсли;
			ФормаВводаПинкода = сбисПолучитьФорму("ФормаВводаПинкода");
			ПараметрыВвода = Новый Структура("СертификатИмя", МестныйКэш.ОбщиеФункции.СформироватьЗаголовокСертификатаДляФормыВвода(МестныйКэш, Сертификат));
			//Всегда активируется первый сертификат списка готовых для активации
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				сбисРезультатВвода = ФормаВводаПинкода.Показать(ПараметрыВвода);
				Если Не ЗначениеЗаполнено(сбисРезультатВвода)
					Или сбисРезультатВвода.ПинКод = "" Тогда
					Сообщить("Не активирован сертификат для подписания документов.");
					Возврат Ложь;	
				КонецЕсли;
				Возврат сбисВыполнитьПереходЗавершение(сбисРезультатВвода, ПараметрыЗавершенияПерехода);
			#Иначе
				ФормаВводаПинкода.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("сбисВыполнитьПереходЗавершение",ЭтаФорма,ПараметрыЗавершенияПерехода);
				ФормаВводаПинкода.Показать(ПараметрыВвода);
				Возврат Неопределено;
			#КонецЕсли
		КонецЦикла;	
	Иначе
		ПараметрыЗавершенияПерехода.СписокСертификатов = Новый СписокЗначений;
	КонецЕсли;
	Возврат сбисВыполнитьПереходЗавершение(Истина, ПараметрыЗавершенияПерехода);
КонецФункции

&НаКлиенте
Функция сбисВыполнитьПереходЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Кэш = ДополнительныеПараметры.Кэш;
	Если Результат = Неопределено Тогда   // не ввели пин-код
		Возврат Ложь;
	КонецЕсли;
	
	// ввели пин-код или не требуется активация
	РезультатВыбораПерехода	= ДополнительныеПараметры.РезультатВыбораПерехода;
	СписокСертификатов		= ДополнительныеПараметры.СписокСертификатов;
	СчСерт					= ДополнительныеПараметры.СчСерт;
	ДействиеВыполнить		= РезультатВыбораПерехода.Действие;
	Если СписокСертификатов.Количество() Тогда
		СертификатДляДействия = СписокСертификатов[СчСерт].Значение;
		СертификатДляДействия.Вставить("КодАктивации", Результат.ПинКод);
	КонецЕсли;
	РезультатАктивации = Кэш.Интеграция.АктивироватьСерверныеСертификаты(Кэш, СписокСертификатов);			
	
	Если РезультатВыбораПерехода.Свойство("Исполнитель") Тогда
		СоставПакета.Этап[0].Вставить("Исполнитель", РезультатВыбораПерехода.Исполнитель);
	КонецЕсли;
	ЭтапВыполнить = Неопределено;
	Если Не РезультатВыбораПерехода.Свойство("Этап", ЭтапВыполнить) Тогда 
		ЭтапВыполнить = СоставПакета.Этап[0];
	КонецЕсли;
	ДействиеВыполнено = МестныйКэш.Интеграция.сбисВыполнитьДействие(МестныйКэш, СоставПакета, ЭтапВыполнить, ДействиеВыполнить, РезультатВыбораПерехода.Комментарий, "");
	Если ДействиеВыполнено = Истина Тогда
		ОбновлятьГлавноеОкно = Истина;
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецФункции


// << alo Меркурий
&НаКлиенте
Процедура СБИСЗаписатьВложение(Кнопка)
	ТабДок = сбисЭлементФормы(ЭтаФорма, "ТаблицаДокументов");
	Если ТабДок.ТекущиеДанные = неопределено тогда
		Сообщить("Нет вложения для записи.");
	Иначе
		Вложение = СоставПакета.Вложение[ТабДок.ТекущиеДанные.НомерВложенияВПакете];
		МестныйКэш.Интеграция.СБИСЗаписатьВложения(МестныйКэш,СоставПакета, Вложение);
	КонецЕсли;
КонецПроцедуры// alo Меркурий >>
// << alo EDI_ДозаписьЮЗДО

&НаКлиенте
Функция СервисДопОперацияПросмотра(СписокДопОпераций) Экспорт 
	Перем ИниСервис;
	Если Не МестныйКэш.Ини.Свойство("Сервис" ,ИниСервис) Тогда
		Возврат Неопределено;
	ИначеЕсли ИниСервис = Неопределено Тогда
		ИниСервис = МестныйКэш.ФормаНастроек.Ини(МестныйКэш, "Сервис");
	КонецЕсли;
	Если Не (	ИниСервис.Свойство("ДопОперацияПросмотра")
			И	СоставПакета.Свойство("Регламент")) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекРегламент = СоставПакета.Регламент;
	Если СоставПакета.Свойство("Этап") И СоставПакета.Этап.Количество() Тогда
		ТекЭтап = СоставПакета.Этап[0].Название;
	Иначе
		ТекЭтап = "";
	КонецЕсли;
	Для Каждого ДопОперация Из ИниСервис.ДопОперацияПросмотра Цикл
		Если Не	(	ДопОперация.Значение.Свойство("Регламент")
				И	ДопОперация.Значение.Свойство("Операция")
				И	ДопОперация.Значение.Операция.Свойство("Функция1С")) Тогда
				Продолжить;
		КонецЕсли;
		Если ДопОперация.Значение.Регламент.Свойство("Идентификатор") И ТекРегламент.Свойство("Идентификатор") Тогда
			РегламентСовпал = (ДопОперация.Значение.Регламент.Идентификатор = ТекРегламент.Идентификатор) ;
		ИначеЕсли ТекРегламент.Свойство("Название") Тогда
			РегламентСовпал = (ДопОперация.Значение.Регламент.Значение = ТекРегламент.Название) ;
		Иначе
			РегламентСовпал = Ложь ;
		КонецЕсли;
		Если Не РегламентСовпал Тогда
			Продолжить;
		КонецЕсли; 
		
		Если ДопОперация.Значение.Свойство("ТребуетсяИдентификатор")
			И НЕ СоставПакета.Свойство("Идентификатор") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтапСовпал =(	ДопОперация.Значение.Свойство("Этап")
					И	ЗначениеЗаполнено(ТекЭтап)
					И	ДопОперация.Значение.Этап.Значение = ТекЭтап);
		ВложениеСовпало = Ложь;
		Если Не ЭтапСовпал И ДопОперация.Значение.Свойство("Вложение") Тогда
			Для Каждого Вложение Из СоставПакета.Вложение Цикл
				Если    Вложение.Свойство("Тип")
					И	Вложение.Тип = ДопОперация.Значение.Вложение.Значение Тогда
					ВложениеСовпало = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если ЭтапСовпал Или ВложениеСовпало или (НЕ ДопОперация.Значение.Свойство("Этап") И НЕ ДопОперация.Значение.Свойство("Вложение")) Тогда
			СписокДопОпераций.Добавить(ДопОперация.Значение.Операция.Функция1С+?(ДопОперация.Значение.Операция.Свойство("Параметры"), "("+ДопОперация.Значение.Операция.Параметры+")", ""), ДопОперация.Значение.Операция.Значение);
		КонецЕсли;
	КонецЦикла;
КонецФункции

&НаСервере
Процедура Договор1СУстановитьОтбор(ИмяРеквизитаКонтрагента)
	// Вставить содержимое обработчика.
	ПараметрыОтбора =Новый массив;
	ПараметрыОтбора.Добавить(Новый СвязьПараметраВыбора("Отбор."+ИмяРеквизитаКонтрагента, "Контрагент1С"));
	ЭтаФорма.Элементы.Договор1С.СвязиПараметровВыбора = Новый ФиксированныйМассив(ПараметрыОтбора);
КонецПроцедуры

&НаКлиенте
Процедура сбисИнформацияДляТП(Команда)
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога); 
	ДиалогОткрытия.МножественныйВыбор = Ложь; 
	ДиалогОткрытия.Заголовок = "Сохранить информации для техподдержки в:"; 
	
	Если ДиалогОткрытия.Выбрать() Тогда 
		ПутьККаталогу = МестныйКэш.ОбщиеФункции.СбисФорматКаталога(ДиалогОткрытия.Каталог, МестныйКэш.ПараметрыСистемы.Клиент);
		Разделитель = МестныйКэш.ОбщиеФункции.СбисФорматРазделителя(МестныйКэш.ПараметрыСистемы.Клиент);
		ПутьККаталогу = ПутьККаталогу+Формат(ТекущаяДата(), "ДФ =гггг_ММ_дд_ЧЧ_мм_сс")+Разделитель;
		СоздатьКаталог(ПутьККаталогу);
		
		Документы1С = Новый СписокЗначений;
		Для Каждого СтрДок Из ТаблицаДокументов	Цикл
			Для Каждого Док1С Из СтрДок.Документы1С Цикл  
				Документы1С.Добавить(Док1С.Значение); 
			КонецЦикла;
		КонецЦикла;
		МестныйКэш.ОбщиеФункции.СбисСформироватьИнформациюПоДокументам1С(МестныйКэш, ПутьККаталогу, Разделитель, Документы1С);
				
		ТекстДок = Новый ТекстовыйДокумент; 
		МестныйКэш.ОбщиеФункции.сбисЗаписатьСтруктуруВТекстовыйДокумент(СоставПакета, ТекстДок, "   ", Новый Массив); 
		ТекстДок.Записать(ПутьККаталогу + "СоставПакета.txt"); 
		
		МестныйКэш.ОбщиеФункции.СбисСформироватьДанныеОтладкиПоПакету(МестныйКэш, ПутьККаталогу, СоставПакета, ИмяРеквизитаВложений);   
		
		МестныйКэш.ОбщиеФункции.СбисСформироватьОбщуюИнформациюДляТП(МестныйКэш, ПутьККаталогу);
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьДанныеСторонИзВложения()
	Если	ЗначениеЗаполнено(СоставПакета)
		И	СоставПакета.Вложение[0].Свойство("ВходящийКонтекст")
		И	СоставПакета.Вложение[0].ВходящийКонтекст.Свойство("ДанныеСторон") Тогда
		
		ДанныеСторон = СоставПакета.Вложение[0].ВходящийКонтекст.ДанныеСторон;
		
		Если ДанныеСторон.Свойство("ДанныеОрганизации")
			И Не ДанныеСторон.ДанныеОрганизации.Ссылка = Неопределено Тогда 
			
			УчетОрганизация = ДанныеСторон.ДанныеОрганизации.Ссылка;
		КонецЕсли;
		Если ДанныеСторон.Свойство("ДанныеКонтрагента")
			И Не ДанныеСторон.ДанныеКонтрагента.Ссылка = Неопределено Тогда
			
			УчетКонтрагент = ДанныеСторон.ДанныеКонтрагента.Ссылка;
		КонецЕсли;
		Если ДанныеСторон.Свойство("ДанныеГрузополучателя")
			И Не ДанныеСторон.ДанныеГрузополучателя.Ссылка = Неопределено Тогда
			
			УчетГрузополучатель = ДанныеСторон.ДанныеГрузополучателя.Ссылка;
		КонецЕсли;
		
	КонецЕсли;	
КонецПроцедуры

// Процедура устанавливает видимость элементов формы на вкладке Прослеживаемость
//
&НаКлиенте
Процедура УстановитьВидимостьЭлементовВкладкиПрослеживаемость()  
	
	СбисРасширение = Неопределено;   
	КодСостоянияОперации = Неопределено;
	лПрослеживаемость = Неопределено;  
	СостояниеПрослеживаемости = Неопределено;
	СостояниеДокумента = Неопределено;
	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ТаблицаПрослеживаемость, "Строки").Очистить();     
	
	ТекущийРаздел = МестныйКэш.Разделы["р"+МестныйКэш.Текущий.Раздел];
	
	Если СоставПакета.Свойство("Расширение", СбисРасширение) Тогда
		Если СбисРасширение.Свойство("Прослеживаемость", лПрослеживаемость)
			И	лПрослеживаемость = "Да" Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"Прослеживаемость").Видимость = Истина;  
		Иначе
            МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"Прослеживаемость").Видимость = Ложь;
		КонецЕсли;
	Иначе
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"Прослеживаемость").Видимость = Ложь;
	КонецЕсли; 
	
	Если СоставПакета.Свойство("Расширение",СбисРасширение) 
		И СбисРасширение.Свойство("СостояниеПросл",СостояниеПрослеживаемости) Тогда
		РасшифровкаСостояния = МестныйКэш.ОбщиеФункции.СостояниеПрослеживаемостиПоКоду(СостояниеПрослеживаемости.КодСостоянияОперации);
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"СтатусДокументаГИСМТ").Заголовок = РасшифровкаСостояния;  
		ЦветСтатуса = ВернутьЦветСтатуса(СостояниеПрослеживаемости.КодСостоянияОперации); 
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"СтатусДокументаГИСМТ").ЦветТекста = ЦветСтатуса;  
	КонецЕсли;  
	
	Если СоставПакета.Свойство("Состояние", СостояниеДокумента)
		И СостояниеДокумента.Свойство("Код")
		И СостояниеДокумента.Код = "7" Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ПроверитьРНПТ").Видимость = Ложь; 
	ИначеЕсли СоставПакета.Свойство("Расширение",СбисРасширение) 
		И СбисРасширение.Свойство("СостояниеПросл",СостояниеПрослеживаемости)
		И СостояниеПрослеживаемости.КодСостоянияОперации = "22" Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ПроверитьРНПТ").Видимость = Ложь;
	Иначе
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ПроверитьРНПТ").Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_ОбработчикиСобытий

//Обработка сопоставления
&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьПриИзменении(ВходящийКэш, ТабЧасть)
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	
	СтрокаТабЧасти	= ТабЧасть.ТекущиеДанные;
	Если СтрокаТабЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ИмяПоиска = "НомерПП";
	#Иначе
		ИмяПоиска = "Название";
		Если ЗначениеЗаполнено(СтрокаТабЧасти.КодПокупателя) Тогда
			ИмяПоиска = "КодПокупателя";
		ИначеЕсли ЗначениеЗаполнено(СтрокаТабЧасти.Идентификатор) Тогда
			ИмяПоиска = "Идентификатор";
		КонецЕсли;
	#КонецЕсли
	ЗначениеПоиска = СтрокаТабЧасти[ИмяПоиска];
	
	ДанныеНоменклатуры = Новый Структура("Название, КодПокупателя, Идентификатор, Код, Номенклатура, Характеристика, ХарактеристикаПоставщика, GTIN, ЕдИзм, ЕдИзмОрг, Коэффициент");
    ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, СтрокаТабЧасти);
	
	ОчищеныКлючиНоменклатура	= Новый Массив;
	УспешноЗаписаноИзменение	= Ложь;
	ЕстьИзмененияСтроки			= Ложь;
	ЕстьИзмененияНоменклатуры	= Ложь;
	Для Каждого КлючИЗначениеСтр Из СтрокаТЧДоИзменения.Номенклатура Цикл
		Если		СтрокаТабЧасти[КлючИЗначениеСтр.Ключ] = КлючИЗначениеСтр.Значение Тогда
			Продолжить;
		ИначеЕсли	КлючИЗначениеСтр.Ключ = "Номенклатура"
			И	Не	ЗначениеЗаполнено(ДанныеНоменклатуры.Номенклатура) Тогда
			КлючиПоискаТЧ = Новый Структура;
			КлючиПоискаТЧ.Вставить("Идентификатор",					ДанныеНоменклатуры.Идентификатор);
			КлючиПоискаТЧ.Вставить("Номенклатура",					СтрокаТЧДоИзменения.Номенклатура.Номенклатура);
			КлючиПоискаТЧ.Вставить("ХарактеристикаНоменклатуры",	СтрокаТЧДоИзменения.Номенклатура.Характеристика);
			ОчищеныКлючиНоменклатура.Добавить("Номенклатура");
			ОчищеныКлючиНоменклатура.Добавить("ХарактеристикаНоменклатуры");
			ОчищеныКлючиНоменклатура.Добавить("ЕдИзмОрг");
			ОчищеныКлючиНоменклатура.Добавить("Коэффициент");
		КонецЕсли;
		ЕстьИзмененияНоменклатуры = Истина;
		ЕстьИзмененияСтроки = Истина;
		Прервать;
	КонецЦикла;
	Если Не ЕстьИзмененияСтроки Тогда
		Для Каждого КлючИЗначениеСтр Из СтрокаТЧДоИзменения.Прочее Цикл
			Если СтрокаТабЧасти[КлючИЗначениеСтр.Ключ] = КлючИЗначениеСтр.Значение Тогда
				Продолжить;
			КонецЕсли;
			ЕстьИзмененияСтроки = Истина;
			Прервать;
		КонецЦикла;
		Если Не ЕстьИзмененияСтроки Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПутьКонтрагента	= сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.ПутьКонтрагента;
	ПутьТаблДок		= сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные.ПутьТаблДок;
	Если Не ЗначениеЗаполнено(ПутьКонтрагента) Тогда
		Сообщить("Отсутствуют требуемые данные для записи сопоставления");
		Возврат;
	КонецЕсли;
	
	//Если меняли номенлклатуру, то запишем.
	Если ЕстьИзмененияНоменклатуры Тогда
		фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("УстановитьСоответствиеНоменклатуры", Кэш.ФормаРаботыСНоменклатурой, "", Кэш);
		Попытка
			фрм.УстановитьСоответствиеНоменклатуры(Кэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьКонтрагента, Вложение.СтруктураФайла), ДанныеНоменклатуры, Кэш.Парам.КаталогНастроек, Кэш.Ини);
			УспешноЗаписаноИзменение = Истина;
		Исключение
			СбисИсключение = Кэш.ОбщиеФункции.СбисИсключение(ИнформацияОбОшибке(), "ФормаПросмотрДокумента.СбисЗаписатьСопоставление",,"Ошибка записи сопоставления");
			Кэш.ГлавноеОкно.СбисСообщитьОбОшибке(Кэш, СбисИсключение, Новый Структура("ФормаВладелец", ЭтаФорма));
		КонецПопытки;
	КонецЕсли;
	
	НомерПП = СтрокаТабЧасти.НомерПП-1;
	ТаблДок = Кэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьТаблДок, Вложение.СтруктураФайла);
	ТаблДок[НомерПП].Вставить("Номенклатура",				ДанныеНоменклатуры.Номенклатура);
	ТаблДок[НомерПП].Вставить("ХарактеристикаНоменклатуры",	ДанныеНоменклатуры.Характеристика);
	ТаблДок[НомерПП].Вставить("ЕдИзмОрг",					ДанныеНоменклатуры.ЕдИзмОрг);
	ТаблДок[НомерПП].Вставить("Коэффициент",				ДанныеНоменклатуры.Коэффициент);
	Если СтрокаТабЧасти.Отмечен Тогда
		ТаблДок[НомерПП].Удалить("НеЗагружать");
	Иначе
		ТаблДок[НомерПП].Вставить("НеЗагружать");
	КонецЕсли;
	
	Если	УспешноЗаписаноИзменение
		И	ОчищеныКлючиНоменклатура.Количество() Тогда
		
		ИмяФормыОбработки = "Файл_Шаблон";
		Если НЕ Вложение.ВерсияФорматаДляЗагрузки = "3_01" Тогда
			ИмяФормыОбработки = ИмяФормыОбработки + "_" + Вложение.ВерсияФорматаДляЗагрузки; 
		КонецЕсли;
		ПараметрыОбработать = Новый Структура("Ключи, Очистить, Таблица", КлючиПоискаТЧ, ОчищеныКлючиНоменклатура, ТаблДок);

		МодульОбработкиВложения = МодульОбъектаКлиент().ПолучитьФормуОбработки(ИмяФормыОбработки);
		МодульОбработкиВложения.ОбработатьТабличнуюЧастьДокумента(ПараметрыОбработать, Кэш);
		
	КонецЕсли;
	ЗаполнитьТаблицуДокументов(СоставПакета);

	Если ЗначениеПоиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтрокаТЧУстановить = Новый Структура(ИмяПоиска, ЗначениеПоиска);
	
	ЭтаФорма.ПодключитьОбработчикОжидания("СбисСпозиционироватьСтроку", 0.1, Истина);
КонецПроцедуры

//Позиционирует строку в ТЧ после обновления
&НаКлиенте
Процедура СбисСпозиционироватьСтроку() Экспорт
	ТабЧасть	= СбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");
	Строка		= ТабличнаяЧасть.НайтиСтроки(СтрокаТЧУстановить);
	Если Строка.Количество() Тогда
		СтрокаУстановить = Строка[0];
		#Если Не ТолстыйКлиентОбычноеПриложение Тогда 
			СтрокаУстановить = СтрокаУстановить.ПолучитьИдентификатор();
		#КонецЕсли
	Иначе
		Возврат;
	КонецЕсли;
	ТабЧасть.ТекущаяСтрока = СтрокаУстановить;		
КонецПроцедуры

//Запись до изменений
&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьДоИзменения(ВходящийКэш, ТабЧасть) 
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	СтрокаТЧДоИзменения = Новый Структура("Номенклатура, Прочее", Новый Структура("Номенклатура, Характеристика, ЕдИзмОрг, Коэффициент"), Новый Структура("Отмечен"));
    ЗаполнитьЗначенияСвойств(СтрокаТЧДоИзменения.Номенклатура,	ТабЧасть.ТекущиеДанные);
    ЗаполнитьЗначенияСвойств(СтрокаТЧДоИзменения.Прочее,		ТабЧасть.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьНоменклатураНачалоВыбора(ВходящийКэш, Элемент, СтандартнаяОбработка)
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("сбисТабличнаяЧастьНоменклатураНачалоВыбора","ФормаПросмотрДокумента","", Кэш);
	Если Не фрм = Ложь Тогда
		фрм.сбисТабличнаяЧастьНоменклатураНачалоВыбора(Кэш, ЭтаФорма, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьХарактеристикаНачалоВыбора(ВходящийКэш, Элемент, СтандартнаяОбработка) 
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	СтандартнаяОбработка=Ложь;
	Если Не ЗначениеЗаполнено(сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").ТекущиеДанные.Номенклатура) Тогда
		Сообщить("Заполните номенклатуру");
	КонецЕсли;
	
	Если Кэш.Ини.Конфигурация.Свойство("ХарактеристикиНоменклатуры") Тогда
		ИмяСправочникаХарактеристикиНоменклатуры = СокрЛП(Сред(Кэш.Ини.Конфигурация.ХарактеристикиНоменклатуры.Значение, Найти(Кэш.Ини.Конфигурация.ХарактеристикиНоменклатуры.Значение, ".")+1));
	Иначе
		ИмяСправочникаХарактеристикиНоменклатуры="ХарактеристикиНоменклатуры";
	КонецЕсли;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Форма = Справочники[ИмяСправочникаХарактеристикиНоменклатуры].ПолучитьФормуВыбора(,Элемент);
		Форма.ПараметрВыборПоВладельцу = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").ТекущиеДанные.Номенклатура;
    #Иначе
		П = Новый Структура();
		Если Лев(ИмяОтбораХарактеристики,6) = "Отбор." Тогда
			ИмяОтбора = Сред(ИмяОтбораХарактеристики, 7);
			П.Вставить("Отбор", Новый Структура(ИмяОтбора,Элементы.ТабличнаяЧасть.ТекущиеДанные.Номенклатура));
		Иначе
			П.Вставить(ИмяОтбораХарактеристики, Элементы.ТабличнаяЧасть.ТекущиеДанные.Номенклатура);
		КонецЕсли;
		Форма = ПолучитьФорму("Справочник." + ИмяСправочникаХарактеристикиНоменклатуры + ".ФормаВыбора", П, Элемент);
	#КонецЕсли
	Форма.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьЕдиницаНачалоВыбора(ВходящийКэш, Элемент, СтандартнаяОбработка) 
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	СтандартнаяОбработка=Ложь;
	Если Не ЗначениеЗаполнено(сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").ТекущиеДанные.Номенклатура) Тогда
		Сообщить("Заполните номенклатуру");
	КонецЕсли;
	
	Если Кэш.Ини.Конфигурация.Свойство("ЕдиницыИзмеренияНоменклатуры") Тогда
		ИмяСправочникаЕдиницыИзмеренияНоменклатуры = СокрЛП(Сред(Кэш.Ини.Конфигурация.ЕдиницыИзмеренияНоменклатуры.Значение, Найти(Кэш.Ини.Конфигурация.ЕдиницыИзмеренияНоменклатуры.Значение, ".")+1));
	Иначе
		ИмяСправочникаЕдиницыИзмеренияНоменклатуры = Кэш.ОбщиеФункции.ТипСправочникаЕдиницПоМетаданным();
	КонецЕсли;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Форма = Справочники[ИмяСправочникаЕдиницыИзмеренияНоменклатуры].ПолучитьФормуВыбора(,Элемент);
		Форма.ПараметрВыборПоВладельцу = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть").ТекущиеДанные.Номенклатура;
    #Иначе
		П = Новый Структура();
		Если Лев(ИмяОтбораХарактеристики,6) = "Отбор." Тогда
			ИмяОтбора = Сред(ИмяОтбораХарактеристики, 7);
			П.Вставить("Отбор", Новый Структура(ИмяОтбора,Элементы.ТабличнаяЧасть.ТекущиеДанные.Номенклатура));
		Иначе
			П.Вставить(ИмяОтбораХарактеристики, Элементы.ТабличнаяЧасть.ТекущиеДанные.Номенклатура);
		КонецЕсли;
		Форма = ПолучитьФорму("Справочник." + ИмяСправочникаЕдиницыИзмеренияНоменклатуры + ".ФормаВыбора", П, Элемент);
	#КонецЕсли
	Форма.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьЕдИзмОргНачалоВыбора(ВходящийКэш, Элемент, СтандартнаяОбработка)      
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;
	
	фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("сбисТабличнаяЧастьЕдИзмОргНачалоВыбора","ФормаПросмотрДокумента","", Кэш);
	Если Не фрм = Ложь Тогда
		фрм.сбисТабличнаяЧастьЕдИзмОргНачалоВыбора(Кэш, ЭтаФорма, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СбисСтрокаТабличнаяЧастьКоэффициентНачалоВыбора(ВходящийКэш, Элемент, СтандартнаяОбработка)  
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	
	фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("сбисТабличнаяЧастьКоэффициентНачалоВыбора","ФормаПросмотрДокумента","", Кэш);
	Если Не фрм = Ложь Тогда
		фрм.сбисТабличнаяЧастьЕдИзмОргНачалоВыбора(Кэш, ЭтаФорма, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СбисВыбратьПочтовыйЯщик(ВходящийКэш, Элемент, ДопПараметры)
	
	
	Перем СписокЯщиков, ЭлементВыбораДефолт;
	
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;

	Отказ = Ложь;
	ИнформацияОКонтрагенте = Кэш.ОбщиеФункции.ПолучитьИнформациюОКонтрагенте(Кэш, СоставПакета.Контрагент, Новый Структура("ДопПоля", "СписокИдентификаторов"), Отказ);
	Если Отказ Тогда
		Кэш.ГлавноеОкно.СбисСообщитьОбОшибке(Кэш, ИнформацияОКонтрагенте, Новый Структура("ФормаВладелец", ЭтаФорма));
		Возврат;
	КонецЕсли;
	Если	Не	ИнформацияОКонтрагенте.Свойство("Идентификатор", СписокЯщиков)
		Или	Не	ТипЗнч(СписокЯщиков) = Тип("Массив")
		Или		СписокЯщиков.Количество() < 2 Тогда
		Кэш.ГлавноеОкно.СбисСообщитьПользователю(Новый Структура("Текст, ФормаВладелец, ЭлементНазначения", "Указывать абонентский ящик не требуется", ЭтаФорма, "АбонентскийЯщик"), Кэш);
		Возврат;
	КонецЕсли;
	СписокВыбора		= Новый СписокЗначений;
	ЭлементВыбораДефолт = Неопределено;
	Для Каждого ЭлементЯщика Из СписокЯщиков Цикл
		ПредставлениеЯщика =	СтрЗаменить(СтрЗаменить(СтрЗаменить("{оператор}({идентификатор}) - {статус}", 
								"{оператор}", ЭлементЯщика.Оператор.Название), 
											"{идентификатор}", ЭлементЯщика.ИдентификаторУчастника),
														"{статус}", ЭлементЯщика.СостояниеПодключения.Описание);
		
		СписокВыбора.Добавить(ЭлементЯщика.ИдентификаторУчастника, ПредставлениеЯщика);
		Если НРег(ЭлементЯщика.Основной) = "да" Тогда
			ЭлементВыбораДефолт = СписокВыбора.НайтиПоЗначению(ЭлементЯщика.ИдентификаторУчастника);
		КонецЕсли;	
	КонецЦикла;
	СписокВыбора.Добавить("", "Определять автоматически");
	
	ПараметрыВыбораСписка = Новый Структура("Заголовок, Элемент, Обработчик", "Укажите абонентский ящик получателя", ЭлементВыбораДефолт, Кэш.ОбщиеФункции.СбисОписаниеОповещения(Кэш, "СбисВыбратьПочтовыйЯщик_Обработчик", ЭтаФорма, Новый Структура("Кэш", Кэш)));
	Кэш.СБИС.МодульОбъектаКлиент.СбисВыбратьИзСписка(СписокВыбора, ПараметрыВыбораСписка);
КонецПроцедуры

&НаКлиенте
Процедура СбисВыбратьПочтовыйЯщик_Обработчик(РезультатВыбора, ДопПараметры) Экспорт
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	АбонентскийЯщик = РезультатВыбора.Представление;
	СоставПакета.Контрагент.Вставить("Идентификатор", РезультатВыбора.Значение);
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасхождениеНажатие_Результат(РезультатОбработки, ДопПараметры) Экспорт

	Если РезультатОбработки = Неопределено Тогда
		
		Возврат;
		
	ИначеЕсли РезультатОбработки.Свойство("code") Тогда
		
		МодульОбъектаКлиент().СообщитьСбисИсключение(РезультатОбработки);
		Возврат;
		
	КонецЕсли;
	
	Если РезультатОбработки.Свойство("ЕстьРасхождения") И РезультатОбработки.ЕстьРасхождения Тогда
		СоставПакета.Вставить("ЕстьРасхождения", Истина);
	КонецЕсли;
	
	СоставПакета.Вложение.Добавить(РезультатОбработки);
	ЗаполнитьТаблицуДокументов(СоставПакета);

КонецПроцедуры                              

&НаКлиенте
Процедура УчетСторонаПриИзменении(Элемент)
	
	Сторона1С = ЭтаФорма[Элемент.Имя];
	Если Не ЗначениеЗаполнено(ДанныеСторон) Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "УчетОрганизация" Тогда
		СторонаДляИзменения = ДанныеСторон.ДанныеОрганизации;
		Тип = "НашаОрганизация";
		СторонаСБИС = СоставПакета.НашаОрганизация;
		ТипДанныхСтороны = "ДанныеОрганизации";
	ИначеЕсли Элемент.Имя = "УчетКонтрагент" Тогда
		СторонаДляИзменения = ДанныеСторон.ДанныеКонтрагента;
		Тип = "Контрагент";
		СторонаСБИС = СоставПакета.Контрагент;
		ТипДанныхСтороны = "ДанныеКонтрагента";
	ИначеЕсли Элемент.Имя = "УчетГрузополучатель" Тогда
		СторонаДляИзменения = ДанныеСторон.ДанныеГрузополучателя;
		Тип = "Контрагент";
		СторонаСБИС = МодульОбъектаКлиент().ПолучитьГрузополучателяПакета(СоставПакета);
		ТипДанныхСтороны = "ДанныеГрузополучателя";
	КонецЕсли;
	
	Если СторонаДляИзменения.Ссылка = Сторона1С Тогда 
		Возврат;
	КонецЕсли;
	
	СторонаДляИзменения.Ссылка = Сторона1С;
	ДанныеДляОбновления = Новый Структура;
	МестныйКэш.ОбщиеФункции.сбисСкопироватьСтруктуруНаКлиенте(ДанныеДляОбновления, СторонаДляИзменения);
	ДанныеДляОбновления.Вставить("Тип", Тип); 
	ДанныеДляОбновления.Вставить("ТипДанныхСтороны", ТипДанныхСтороны);
	ДопПараметры = Новый Структура("СторонаСБИС", СторонаСБИС);
	МодульОбъектаКлиент().Маппинг_ОбновитьСторону1С(ДанныеДляОбновления, ДопПараметры);
	
КонецПроцедуры   

// Процедура проверяет введенный номер госконтракта и изменяет его в документе СБИС
//
// Параметры:
//  Элемент	 - 	 - 
//  Текст	 - Строка - Строка текста, введенная в поле ввода.
//
&НаКлиенте
Процедура ИдентификаторГосконтрактаОкончаниеВводаТекста(Элемент, Текст)   
	
	Если СтрДлина(СокрЛП(Текст)) < 20 Тогда
		МодульОбъектаКлиент().СбисСообщить("№ контракта должен содержать от 20 до 25 символов"); 
		Возврат;
	КонецЕсли;  
	
	ПараметрыДокумента = Новый Структура("ИдентификаторГосконтракта", СокрЛП(Текст));
	
	Попытка
		МодульОбъектаКлиент().ИзменитьСкладскойПараметрДокумента(СоставПакета, ПараметрыДокумента);
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ИдентификаторГосконтрактаОкончаниеВводаТекста");
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область include_local_Формы_ФормаПросмотрДокумента_События_ТабличнаяЧасть

&НаКлиенте
Процедура ТабличнаяЧастьНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СбисСтрокаТабличнаяЧастьНоменклатураНачалоВыбора(МестныйКэш, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьЕдИзмОргНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СбисСтрокаТабличнаяЧастьНоменклатураНачалоВыбора(МестныйКэш, Элемент, СтандартнаяОбработка);
	СбисСтрокаТабличнаяЧастьЕдиницаНачалоВыбора(МестныйКэш, Элемент, СтандартнаяОбработка);	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьКоэффициентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СбисСтрокаТабличнаяЧастьКоэффициентНачалоВыбора(МестныйКэш, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СбисСтрокаТабличнаяЧастьХарактеристикаНачалоВыбора(МестныйКэш, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_События_Форма

// Процедура - событие завершения отправки
//
// Параметры:
//  РезультатОтправки	 - РезультатОтправки	 - Экземпляр класса отправки
//  ДопПараметры		 - Структура	 - Кэш
//
&НаКлиенте
Процедура ОтправитьДокументы_Завершение(РезультатОтправки, ДопПараметры) Экспорт
	
	ПослеОтправки(Кэш);
	
КонецПроцедуры

// Процедура - событие после отправки
//
// Параметры:
//  ВходящийКэш	 - ЛокальныйКэш	 - структура, есл ине определен кэш на форме
//
&НаКлиенте
Процедура ПослеОтправки(ВходящийКэш) Экспорт 
	Если Кэш = Неопределено Тогда
		Кэш = ВходящийКэш;
	КонецЕсли;
	// Если есть ошибки отправки, то выводим результат, иначе закрываем просмотр 	
	Если Кэш.РезультатОтправки.НеОтправлено>0 Тогда  // если есть ошибки при отправке, показываем окно результатов
		фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("ПоказатьРезультатОтправки","ФормаРезультатОтправки","", Кэш);
		фрм.ПоказатьРезультатОтправки(Кэш);	
	Иначе    // если отправилось без ошибок, закрываем окно просмотра
		ОбновлятьГлавноеОкно = Истина;
		Если ЭтаФорма.Открыта() Тогда
			ЭтаФорма.Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_События_ТабличнаяЧасть

&НаКлиенте
Процедура ТабличнаяЧастьПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТабЧасть = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");
	СбисСтрокаТабличнаяЧастьПриИзменении(МестныйКэш, ТабЧасть);	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТабЧасть = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");
	СбисСтрокаТабличнаяЧастьДоИзменения(МестныйКэш, ТабЧасть);
КонецПроцедуры

#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_Команды

&НаКлиенте
Процедура КонтактыТП(Команда)
	ЗапуститьПриложение("https://sbis.ru/support");
КонецПроцедуры

&НаКлиенте
Процедура ОбращениеТП(Команда)
	ЗапуститьПриложение("https://online.sbis.ru/page/my-claims");
КонецПроцедуры

// Процедура отправляет текущий пакет документов 	
&НаКлиенте
Процедура ОтправитьНажатие(Команда)
	
	Перем Идентификатор, БлокировкаОтправки, СтатусЭД;
	
	СоставПакета.Вставить("Примечание", ПакетКомментарий); 
	
	Если	СоставПакета.Вложение.Количество()
		И	СоставПакета.Вложение[0].Свойство("Документы1С") Тогда
		фрм = Кэш.ГлавноеОкно.сбисНайтиФормуФункции("СбисПрочитатьЗначениеСвойстваДокумента",Кэш.ФормаРаботыСоСтатусами,"",Кэш);
		ДокументСсылка = СоставПакета.Вложение[0].Документы1С[0].Значение;   
		ПараметрыЧтенияСвойства    = Новый Структура("Имя, ДокументСсылка", "ДокументСБИС_Статус", ДокументСсылка);
	    ПредставлениеСтатуса = фрм.СбисПрочитатьЗначениеСвойстваДокумента(ПараметрыЧтенияСвойства,Кэш); 
		СтатусЭД = Кэш.ОбщиеФункции.СбисИндексКартинкиПоСтатусуЭД(ПредставлениеСтатуса); 
	КонецЕсли;
	
	Если СоставПакета.Свойство("Идентификатор") Тогда
		Идентификатор = СоставПакета.Идентификатор;
	КонецЕсли; 
	
	Если НЕ Идентификатор = Неопределено Тогда     
		Попытка
			БлокировкаОтправки = МодульОбъектаКлиент().ПроверитьБлокировкуОтправкиПакетаВСБИС(СоставПакета);
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ОтправитьНажатие");
		КонецПопытки; 
	КонецЕсли; 
	
	Если НЕ БлокировкаОтправки = Неопределено И БлокировкаОтправки.БлокироватьОтправку Тогда
		МодульОбъектаКлиент().СбисСообщить(БлокировкаОтправки.ПричинаБлокировки);
		Возврат;
	КонецЕсли;

	МассивПодготовленныхПакетов = Новый Массив;
	МассивПодготовленныхПакетов.Добавить(СоставПакета);
	СписокКОтправке = Новый Массив;
	СписокКОтправке.Добавить(Новый Структура("МассивПодготовленныхПакетов,ИдПакета,Статус", МассивПодготовленныхПакетов,Идентификатор,СтатусЭД));
	
	ПараметрыЗавершить			= Новый Структура("Кэш", Кэш);
	ОбработчикРезультатОтправки	= МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("ОтправитьДокументы_Завершение", ЭтаФорма, ПараметрыЗавершить);

	ДопПараметрыОтправки = Новый Структура("ИмяРеестра, ОбработчикРезультата", Кэш.Текущий.ТипДок, ОбработчикРезультатОтправки);
	
	МодульОбъектаКлиент().ЗапуститьМассовуюОтправкуДокументов(СписокКОтправке, ДопПараметрыОтправки)

КонецПроцедуры

&НаКлиенте
Процедура РуководствоПользователя(Команда)
	ЗапуститьПриложение("https://sbis.ru/help/integration/1C_set/modul/docs");
КонецПроцедуры

&НаКлиенте
Процедура АбонентскийЯщикНажатие(Команда, СтандартнаяОбработка=Истина)
	Элемент = сбисЭлементФормы(ЭтаФорма, "АбонентскийЯщик");
	СтандартнаяОбработка = Ложь;
	СбисВыбратьПочтовыйЯщик(МестныйКэш, Элемент, Новый Структура);
КонецПроцедуры

// Процедура - обработчик события ПриСменеСтраницы основной панели  
//
&НаКлиенте
Процедура ГлавнаяПанельПриСменеСтраницы(Элемент, ТекущаяСтраница)
	РасширениеСостава = Неопределено; 
	Маркировка = Неопределено;
	лПрослеживаемость = Неопределено;
	
	ИмяСтраницы = Элемент.ТекущаяСтраница.Имя;
	
	Если ИмяСтраницы = "Маркировка"
		И СоставПакета.Свойство("Расширение",РасширениеСостава)
		И	РасширениеСостава.Свойство("Маркировка", Маркировка)
		И	Маркировка = "Да" 
		И НЕ ПараметрыРаботы.МаркировкаЗаполнена Тогда //Запретил повторное перечитываение
		ЗаполнитьМаркировку(СоставПакета); 
	ИначеЕсли ИмяСтраницы = "Прослеживаемость"
		И СоставПакета.Свойство("Расширение",РасширениеСостава)
		И	РасширениеСостава.Свойство("Прослеживаемость", лПрослеживаемость)
		И	лПрослеживаемость = "Да" 
		И НЕ ПараметрыРаботы.ПрослеживаемостьЗаполнена Тогда //Запретил повторное перечитываение
		ЗаполнитьПрослеживаемость(СоставПакета);  
	Иначе
		//
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик разворачивания строки списка маркируемых позиций 
//
&НаКлиенте
Процедура ТаблицаМаркировкаПередРазворачиванием(Элемент, Строка, Отказ)  
	
	Если НЕ ПараметрыРаботы.Свойство("МаркировкаЗаполнена") Тогда
		Возврат;
	КонецЕсли;
	
	#Если Не ТолстыйКлиентОбычноеПриложение Тогда
		Строка = ТаблицаМаркировка.НайтиПоИдентификатору(Строка); 
	#КонецЕсли
	Если НЕ Строка = Неопределено Тогда  
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(Строка, "Строки").Очистить();
		
		ТаблицаФормы = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма, "ТаблицаМаркировка");
		
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаФормы, "КодМаркировки").Видимость = Истина;
		МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ТаблицаФормы, "ИндикаторКодаМаркировки").Видимость = Истина; 
		
		Попытка
			ПараметрыНоменклатуры = МодульОбъектаКлиент().ВернутьПараметрыНоменклатуры(СоставПакета,Новый Структура("НоменклатураСбис",Строка.НоменклатураСбис));  //Если поэкземплярный учет, то вычитываем НДР
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ТаблицаМаркировкаПередРазворачиванием");
		КонецПопытки;
		
		Если ПараметрыНоменклатуры = Неопределено ИЛИ НЕ ПараметрыНоменклатуры.Количество() Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого ЭлСтр ИЗ ПараметрыНоменклатуры Цикл
			НоваяСтрока = МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(Строка, "Строки").Добавить();   
			НоваяСтрока.КодМаркировки = ЭлСтр.Number;
			Если ЭлСтр.Свойство("IndicatorData") 
				И ТипЗнч(ЭлСтр.IndicatorData) = Тип("Массив")
				И ЗначениеЗаполнено(ЭлСтр.IndicatorData) Тогда
				НоваяСтрока.ИндикаторКодаМаркировки = ЭлСтр.IndicatorData[0].Error;    
				НоваяСтрока.ИндексКартинкиКод = 1;
			КонецЕсли;
		КонецЦикла;  
	КонецЕсли;
КонецПроцедуры

// Процедура - устанавливает выбранный параметр причины выбытия кодов маркировки
// 
&НаКлиенте
Процедура ПричинаВыводаПриИзменении()
	Перем КодПричиныВывода;  
	
	ТекущийРаздел = МестныйКэш.Разделы["р"+МестныйКэш.Текущий.Раздел]; 
	
	СкладскиеПараметрыДокумента = МодульОбъектаКлиент().ПолучитьСкладскиеПараметрыДокумента(СоставПакета); 
	
	//ПричинаВывода = СкладскиеПараметрыДокумента.ПричинаВывода; 
	 
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "ПричинаВывода").СписокВыбора.ЗагрузитьЗначения(СкладскиеПараметрыДокумента.ПричиныВыводаИзОборота); 
		
	МассивПричинВыводаИзОборота = МодульОбъектаКлиент().ПричиныВыводаИзОборота();  
	
	Если СкладскиеПараметрыДокумента.ПричиныВыводаИзОборота.Найти(ПричинаВывода) = Неопределено Тогда 
		//Причина нам не подходит, т.к. не применима к текущему параметру участника оборота ГИС МТ 
		ПричинаВывода = МассивПричинВыводаИзОборота[0].Причина;
	КонецЕсли;
	
	Индекс = Кэш.ОбщиеФункции.ВыбратьДанныеСертификата(МассивПричинВыводаИзОборота,ПричинаВывода,"Причина");
	
	//Здесь, возможно, необходимо будет поменять на причину вывода по умолчанию.
	Если Индекс = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	КодПричиныВывода = МассивПричинВыводаИзОборота[Индекс].КодПричины;
	
	Если КодПричиныВывода = Неопределено Тогда
		Возврат; 
	ИначеЕсли КодПричиныВывода = "1"
		И ТекущийРаздел = "Продажа" Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ИдентификаторГосконтракта").Видимость = Истина;
		Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьНомерКонтракта").Видимость = Истина;
		КонецЕсли;
	Иначе
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ИдентификаторГосконтракта").Видимость = Ложь;
		Если НЕ МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
			МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"НадписьНомерКонтракта").Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыДокумента = Новый Структура("КодПричины", КодПричиныВывода); 
	
	Попытка
		МодульОбъектаКлиент().ИзменитьСкладскойПараметрДокумента(СоставПакета, ПараметрыДокумента);
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ПричинаВыводаПриИзменении");
	КонецПопытки;
	
КонецПроцедуры

// Процедура - устанавливает выбранный параметр участника оборота маркируемой продукции 
//
&НаКлиенте
Процедура УчастникОборотаПриИзменении(Элемент)
	Перем КодУчастикаОборота;
	
	МассивУчастниковОборота = МодульОбъектаКлиент().ПараметрыУчастиковОборота();  
	Для Каждого ЭлМасс ИЗ МассивУчастниковОборота Цикл
		Если ЭлМасс.Причина = УчастникОборота Тогда 
			КодУчастикаОборота = ЭлМасс.КодПричины;
			Прервать; 
		КонецЕсли;
	КонецЦикла;
	
	Если КодУчастикаОборота = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДокумента = Новый Структура("КодУчастикаОборота", КодУчастикаОборота); 
	
	Попытка
		МодульОбъектаКлиент().ИзменитьСкладскойПараметрДокумента(СоставПакета, ПараметрыДокумента);
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "УчастникОборотаПриИзменении");
	КонецПопытки;
	
	МестныйКэш.ГлавноеОкно.СбисПолучитьЭлементФормы(ЭтаФорма,"ОтправитьКодыМаркировки").Видимость = КодУчастикаОборота;  
	
	ПричинаВыводаПриИзменении();

КонецПроцедуры

// Процедура - запускает проверку наличия токена ГИС МТ и инициирует проверку кодов маркировки 
//
&НаКлиенте
Процедура ЗапуститьПроверкуКодовМаркировки(Элемент)  
	
	Перем РезультатПроверкиТокена;
	
	Попытка
		РезультатПроверкиТокена = МодульОбъектаКлиент().ПроверитьНаличиеТокена(СоставПакета);
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ЗапуститьПроверкуКодовМаркировки");
	КонецПопытки; 
	
	Если РезультатПроверкиТокена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	  
	ОбработатьРезультатПроверкиТокена(РезультатПроверкиТокена); 
	
КонецПроцедуры 

// Процедура - инициирует проверку кодов маркировки или вызывает создание токена ГИС МТ 
//
// Параметры:
//  РезультатПроверкиТокена	 - Структура - результат проверки токена и описание ошибки
//
&НаКлиенте
Процедура ОбработатьРезультатПроверкиТокена(РезультатПроверкиТокена)  
	
	Перем РезультатНаличияТокена;
	
	Если РезультатПроверкиТокена.Свойство("Result",РезультатНаличияТокена)
		И НЕ РезультатНаличияТокена = Неопределено
		И ВРЕГ(РезультатНаличияТокена) = "OK" Тогда   	
		Попытка
			МодульОбъектаКлиент().ИнициироватьПроверкуКодовМаркировки(СоставПакета); 
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ОбработатьРезультатПроверкиТокена");
		КонецПопытки; 	
		ЗаполнитьМаркировку(СоставПакета); 
	Иначе
		ОбработчикДиалога = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("СоздатьТокенГИСМТ", ЭтаФорма);
		ТекстВопроса = "Токен, для проверки сведений ГИС МТ, не найден. Создать новый?";
		МодульОбъектаКлиент().СбисПоказатьВопрос(ОбработчикДиалога, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - получает список сертификатов и запускает окно выбора сертификата для создания токена 
//
&НаКлиенте
Процедура СоздатьТокенГИСМТ(РезультатДиалога,ДопПараметры = Неопределено) Экспорт  
	ТекстОшибки = "";
	
	Если Не РезультатДиалога = КодВозвратаДиалога.Да Тогда	
		Возврат;
	КонецЕсли;
	
	Если Не МестныйКэш.СБИС.ОбменВключен Тогда
		Ошибка = Новый Структура("code, message, details", 100, "Не включен обмен со СБИС", "Сертификаты не найдены. Выберите способ обмена в настройках соединения.");
		МестныйКэш.ГлавноеОкно.сбисСообщитьОбОшибке(МестныйКэш, Ошибка, Новый Структура("ФормаВладелец, Отправлять", ЭтаФорма, Ложь));
		ТекстОшибки = Ошибка.message;
	КонецЕсли;
	
	//Получим список
	Если ТекстОшибки = "" Тогда
		СписокСертификатовСырой = МестныйКэш.Интеграция.ПолучитьСписокСертификатовДляАвторизации(МестныйКэш,ТекстОшибки);
	КонецЕсли;
	Если	ТекстОшибки = ""
		И	Не	ЗначениеЗаполнено(СписокСертификатовСырой) Тогда
		ТекстОшибки = "Сертификаты не найдены";
	КонецЕсли;  
	
	ОбработчикВыбора = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("РезультатВыбораСертификата", ЭтаФорма);
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ФормаВыборИзСписка = сбисПолучитьФорму("Форма_ВыборИзСписка");
		ФормаВыборИзСписка.СписокДляВыбора = СписокСертификатовСырой;
		ФормаВыборИзСписка.Заголовок ="Выберите сертификат для создания токена ГИС МТ";
		Ответ = ФормаВыборИзСписка.ОткрытьМодально();
		РезультатВыбораСертификата(Ответ);
	#Иначе
		ОткрытьФорму(МестныйКэш.ГлавноеОкно.СбисПутьКФормамОбработки() + "Форма_ВыборИзСписка",Новый Структура("СписокДляВыбора, Заголовок",СписокСертификатовСырой, "Выберите сертификат для создания токена ГИС МТ"),,,,,ОбработчикВыбора);
    #КонецЕсли
КонецПроцедуры   

// Функция - обрабатывает результат выбора сертификата для создания токена  
//
&НаКлиенте
Функция РезультатВыбораСертификата(Результат, ДопПараметры = Неопределено) Экспорт   
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("Сертификат",Результат);
	ФормаВводаПинкода = МестныйКэш.ГлавноеОкно.сбисПолучитьФорму("ФормаВводаПинкода");   
	Если НЕ Результат = Неопределено Тогда
		ПараметрыВвода = Новый Структура("СертификатИмя", Результат.Название);
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			КодАктивации = ФормаВводаПинкода.Показать(ПараметрыВвода);
			Если Не	ЗначениеЗаполнено(КодАктивации)	Или КодАктивации.ПинКод = "" Тогда
				Сообщить("Не активирован сертификат для подписания документов.");
				Возврат Неопределено;	
			КонецЕсли;		
			Возврат РезультатВводаПинКода(Новый Структура("ПинКод",КодАктивации.ПинКод),ДопПараметры);		
		#Иначе
			ФормаВводаПинкода.ОписаниеОповещенияОЗакрытии = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("РезультатВводаПинКода",ЭтаФорма,ДопПараметры);
			ФормаВводаПинкода.Показать(ПараметрыВвода);
		#КонецЕсли 
	КонецЕсли;
КонецФункции

// Функция - обрабатывает результат ввода пин кода сертификата для создания токена  
//  
&НаКлиенте
Функция РезультатВводаПинКода(Результат, ДопПараметры = Неопределено) Экспорт    
	ПинКод = Неопределено;
	Отпечаток = Неопределено;
	
	Если Результат = Неопределено Тогда  
		МодульОбъектаКлиент().СбисСообщить("Не активирован сертификат для подписания документов.");
		Возврат Ложь; 
	КонецЕсли;
		
	Если Результат.Свойство("ПинКод",ПинКод) 
		И НЕ ДопПараметры = Неопределено
		И ДопПараметры.Свойство("Сертификат")
		И ДопПараметры.Сертификат.Свойство("Отпечаток",Отпечаток) Тогда  
		Попытка
			ДанныеСертификатаСбис = МодульОбъектаКлиент().ПолучитьДанныеСертификатаСбис(Отпечаток); 
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "РезультатВводаПинКода");
		КонецПопытки; 
		Если НЕ ЗначениеЗаполнено(ДанныеСертификатаСбис) Тогда 
			МодульОбъектаКлиент().СбисСообщить("Сертификат, по отпечатку, не найден в СБИС");
			Возврат Ложь;
		КонецЕсли;
		ИндексСтрокиХэшаАлгоритма = МестныйКэш.ОбщиеФункции.ВыбратьДанныеСертификата(ДанныеСертификатаСбис.s,"HashAlgOID","n"); 
		ИндексСтрокиДвоичныхДанных = МестныйКэш.ОбщиеФункции.ВыбратьДанныеСертификата(ДанныеСертификатаСбис.s,"Файл","n"); 
		Если ИндексСтрокиХэшаАлгоритма = Неопределено
			И ИндексСтрокиДвоичныхДанных = Неопределено Тогда
			МодульОбъектаКлиент().СбисСообщить("Сертификат, по отпечатку, не найден в СБИС");
			Возврат Ложь; 
		КонецЕсли;
		ХэшАлгоритмаШифрования = ДанныеСертификатаСбис.d[ИндексСтрокиХэшаАлгоритма];
		ДвоичныеДанныеСертификата = ДанныеСертификатаСбис.d[ИндексСтрокиДвоичныхДанных];
		ПараметрыСертификата = Новый Структура();   
		ПараметрыСертификата.Вставить("thumbprint",ДопПараметры.Сертификат.Отпечаток);
		ПараметрыСертификата.Вставить("hashOID",ХэшАлгоритмаШифрования);
		ПараметрыСертификата.Вставить("pin",Результат.ПинКод);  
		ПараметрыСертификата.Вставить("validFrom",ДопПараметры.Сертификат.ДействителенС);
		ПараметрыСертификата.Вставить("validTo",ДопПараметры.Сертификат.ДействителенПо);  
		ПараметрыСертификата.Вставить("certData",ДвоичныеДанныеСертификата); 
		Попытка
			МодульОбъектаКлиент().СоздатьТокенСбис(СоставПакета,ПараметрыСертификата);  
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "РезультатВводаПинКода");
		КонецПопытки;
		ОбработатьРезультатСозданияТокена();
	Иначе 
		Возврат Ложь;
	КонецЕсли;
КонецФункции 

// Процедура - обрабатывает результат создания токена в ГИС МТ  
//  
&НаКлиенте
Процедура ОбработатьРезультатСозданияТокена()   
	Результат = Неопределено;
	ТокенСоздан = Ложь;
	ВремяВызова = ТекущаяДата();
	Пока Истина Цикл
		Попытка
			Результат = МодульОбъектаКлиент().ПолучитьРезультатСозданияТокена(СоставПакета);   
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ОбработатьРезультатСозданияТокена");
		КонецПопытки;

		Если Не Результат = Неопределено 
			И Результат.Свойство("Result") 
			И ВРЕГ(Результат.Result) = "OK" Тогда 
			//Получен ответ на запрос, или делаем один проход на чтение	  
			ТокенСоздан = Истина;
			Прервать;
		ИначеЕсли ТекущаяДата() - ВремяВызова > 10 Тогда
			//Установленный на команде таймаут
			Отказ = Истина;
			МодульОбъектаКлиент().СообщитьСбисИсключение("ExtSysMarking.CheckGisTask() не вернул ответ за разумное время", "ОбработатьРезультатСозданияТокена");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ТокенСоздан Тогда
    	ЗаполнитьМаркировку(СоставПакета);
	КонецЕсли;
КонецПроцедуры   

// Функция - возвращает значение типа цвет, в зависимости результата кода состояния проверки документа 
//
&НаКлиенте
Функция ВернутьЦветСтатуса(КодСостоянияОперации) 
	МассивОшибок = Новый Массив; 
	МассивОшибок.Добавить("10");  
	МассивОшибок.Добавить("13");
	МассивОшибок.Добавить("20");
	МассивОшибок.Добавить("23");
	МассивОшибок.Добавить("30");
	
	МассивУспех = Новый Массив; 
	МассивУспех.Добавить("3"); 
	МассивУспех.Добавить("22");
	
	ИндексОшибок = МассивОшибок.Найти(КодСостоянияОперации);
	ИндексУспех = МассивУспех.Найти(КодСостоянияОперации);
	
	Если НЕ ИндексОшибок = Неопределено Тогда 
		Возврат Новый Цвет(255,0,0);   
	ИначеЕсли НЕ ИндексУспех = Неопределено Тогда
		Возврат Новый Цвет(0,128,0); 
	Иначе 
		Возврат Новый Цвет(0,0,0);;
	КонецЕсли;
	
КонецФункции

// Процедура - запускает проверку статуса документа в ГИС МТ. 
//
&НаКлиенте
Процедура СтатусДокументаГИСМТ(Элемент)  
	
	Попытка
		Результат = МодульОбъектаКлиент().ПроверитьСтатусПроверкиГИСМТ(СоставПакета);    
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "СтатусДокументаГИСМТ");
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыСообщения = Новый Структура;  
	ПараметрыСообщения.Вставить("АдресСсылка",);  
	ПараметрыСообщения.Вставить("ТекстСсылка",);
	Если Результат.Messages.Количество() Тогда
		ПараметрыСообщения.Вставить("Текст", Результат.Messages[0][0]);
		сбисПолучитьФорму("ФормаПредупреждения").Показать(МестныйКэш, ПараметрыСообщения); 
	КонецЕсли;
		
КонецПроцедуры

// Процедура - запускает отправку кодов документа в ГИС МТ. 
//  
&НаКлиенте
Процедура ОтправитьКодыМаркировки(Элемент)  
	
	Перем Идентификатор, БлокировкаОтправки; 
	
	Если СоставПакета.Свойство("Идентификатор") Тогда
		Идентификатор = СоставПакета.Идентификатор;
	КонецЕсли; 
	
	Если НЕ Идентификатор = Неопределено Тогда     
		Попытка
			БлокировкаОтправки = МодульОбъектаКлиент().ПроверитьБлокировкуОтправкиПакетаВСБИС(СоставПакета);
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ОтправитьНажатие");
		КонецПопытки; 
	КонецЕсли; 
	
	Если НЕ БлокировкаОтправки = Неопределено И БлокировкаОтправки.БлокироватьОтправку Тогда
		МодульОбъектаКлиент().СбисСообщить(БлокировкаОтправки.ПричинаБлокировки);
		Возврат;
	КонецЕсли;
	
	Попытка
		Результат = МодульОбъектаКлиент().ОтправитьКодыМаркировкиВГИС(СоставПакета);    
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ОтправитьКодыМаркировки");   
		Возврат;
	КонецПопытки;    
	
	Если Результат Тогда  
		сбисЭлементФормы(ЭтаФорма,"СтатусДокументаГИСМТ").ЦветТекста = Новый Цвет(28,85,174);
		сбисЭлементФормы(ЭтаФорма,"СтатусДокументаГИСМТ").Заголовок = "Регистрация в ГИС МТ"; 
		ЗаполнитьМаркировку(СоставПакета); 
	КонецЕсли;
	
КонецПроцедуры

// Процедура запускает проверку кодов РНПТ
// 
&НаКлиенте
Процедура ПроверитьРНПТ(Команда)
	
	Попытка
		Результат = МодульОбъектаКлиент().ПроверитьПрослеживаемыеПозиции(СоставПакета);
	Исключение
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ПроверитьРНПТ");
	КонецПопытки; 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	РасшифровкаСостояния = МестныйКэш.ОбщиеФункции.СостояниеПрослеживаемостиПоКоду(Строка(Результат));
	сбисЭлементФормы(ЭтаФорма,"СтатусДокументаГИСМТ").Заголовок = РасшифровкаСостояния;  
	ЦветСтатуса = ВернутьЦветСтатуса(Строка(Результат)); 
	сбисЭлементФормы(ЭтаФорма,"СтатусДокументаГИСМТ").ЦветТекста = ЦветСтатуса;  
	
	ЗаполнитьПрослеживаемость(СоставПакета);
	
КонецПроцедуры


#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_РасширенноеСопоставление_СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОперацииПодготовкиКЗагрузкеРасширенноеСопоставление()
	
	ЗаполнитьДанныеНоменклатурДляРасширенногоСопоставления();
	
	ЭлементФормыХарактеристика = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьХарактеристика");
	Если Не МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		ЭлементФормыХарактеристика = ЭтаФорма.ЭлементыФормы.ТабличнаяЧасть.Колонки.Характеристика;
	КонецЕсли; 

	ЭлементФормыХарактеристикаПоставщика = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧастьХарактеристика");
	Если Не МестныйКэш.ПараметрыСистемы.Клиент.УправляемоеПриложение Тогда
		ЭлементФормыХарактеристикаПоставщика = ЭтаФорма.ЭлементыФормы.ТабличнаяЧасть.Колонки.Характеристика;
	КонецЕсли;
	
	ЭлементФормыХарактеристика.Видимость = Ложь;
	ЭлементФормыХарактеристикаПоставщика.Видимость = Ложь;
	
	ОбновитьСтатусыСопоставления();

	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ТабличнаяЧастьЗаписатьСопоставлениеНоменклатуры").Доступность = Истина;
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ТабличнаяЧастьОткрытьФормуСопоставления").Доступность = Истина;
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ТабличнаяЧастьСоздатьСопоставитьНоменклатуруВ1С").Доступность = Истина;
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма,"ОтметитьВсе").Доступность = Истина;
	
КонецПроцедуры

// функция для совместимости кода 
&НаКлиенте
Функция СбисПолучитьФормуСтар(СбисИмяФормы, Объект1С = Неопределено, ПараметрыФормы = Неопределено, СбисВладелецФормы = Неопределено) Экспорт
	
	Попытка
		ПараметрыЗапросаФормы = Новый Структура;
		ПараметрыЗапросаФормы.Вставить("Владелец",			СбисВладелецФормы);
		ПараметрыЗапросаФормы.Вставить("ОбработкаОбъект",	Объект1С);
		ПараметрыЗапросаФормы.Вставить("Параметры",			ПараметрыФормы);
		Возврат МодульОбъектаКлиент().ПолучитьФормуОбработки(СбисИмяФормы, ПараметрыЗапросаФормы);
	Исключение
		Результат		= Ложь;
		ИнфоОбОшибке	= ИнформацияОбОшибке();
		ОшибкаПолученияФормы= МодульОбъектаКлиент().НовыйСбисИсключение(ИнфоОбОшибке, "ФормаГлавноеОкно.СбисПолучитьФорму");
		ПолноеИмяФормы		= МодульОбъектаКлиент().ПолучитьФормуОбработки_ПолноеИмяФормы(СбисИмяФормы, Объект1С);
		БезопасноеИмяФормы	= МодульОбъектаКлиент().ПолучитьФормуОбработки_БезопасноеИмяФормы(ПолноеИмяФормы, Объект1С);
		
		МодульОбъектаКлиент().СообщитьСбисИсключение(ОшибкаПолученияФормы);
		МодульОбъектаКлиент().ПолучитьФормуОбработки_ЗакэшироватьФорму(Результат, БезопасноеИмяФормы);
		Возврат Результат;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПослеРучнойОбработкиСопоставления(Результат, ДопПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПоСтрокеСопоставления = Результат;
	
	ОбогащаемаяНоменклатура = Неопределено;
	
	Для Каждого СтрокаСопоставления Из ДанныеНоменклатурДляРасширенногоСопоставления.ПодготовленныеДанныеНоменклатур Цикл
		БезопасноеИмяИдентификатора = ПолучитьБезопасноеНаименование(СтрокаСопоставления.НоменклатураСБИС.Наименование); 
		СтрокаСопоставленияПослеРедактирования = Новый Структура;
		Если ДанныеПоСтрокеСопоставления.Свойство(БезопасноеИмяИдентификатора, СтрокаСопоставленияПослеРедактирования) Тогда
			СтрокаСопоставления.Номенклатура1С = СтрокаСопоставленияПослеРедактирования.Номенклатура1С;
			ОбогащаемаяНоменклатура = СтрокаСопоставления;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтборНоменклатуры = Новый Структура();
	ОтборНоменклатуры.Вставить("Название", ОбогащаемаяНоменклатура.НоменклатураСБИС.Наименование); 
	ИскомыеСтрокиНоменклатуры = ТабличнаяЧасть.НайтиСтроки(ОтборНоменклатуры);
	
	Если ИскомыеСтрокиНоменклатуры.Количество() Тогда  
		Для Каждого Номенклатура Из ИскомыеСтрокиНоменклатуры Цикл
			Номенклатура.Статус = 2;
		КонецЦикла;
	Иначе
		Сообщить("Запись ручного сопоставления прошла с ошибкой. Номенклатура 1С не записалась");
	КонецЕсли;
	
	СоставПакета.Вставить("ДопПараметрыСопоставления", ДанныеНоменклатурДляРасширенногоСопоставления); 
	ЗаполнитьТаблицуДокументов(СоставПакета);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусыСопоставления()
	
	Если НЕ ДанныеНоменклатурДляРасширенногоСопоставления.Свойство("ПодготовленныеДанныеНоменклатур") Тогда
		Возврат;
	КонецЕсли;
	ОтборНоменклатуры = Новый Структура();
	
	Для Каждого ПозицияНоменклатурыСБИС Из ДанныеНоменклатурДляРасширенногоСопоставления.ПодготовленныеДанныеНоменклатур Цикл
		
		ОтборНоменклатуры.Вставить("Название", ПозицияНоменклатурыСБИС.НоменклатураСБИС.Наименование); 
		НоменклатураВТабличнойЧасти = ТабличнаяЧасть.НайтиСтроки(ОтборНоменклатуры);
		Если НоменклатураВТабличнойЧасти.Количество() Тогда               
			Для Каждого СтрокаНоменклатуры Из НоменклатураВТабличнойЧасти Цикл  
				Если СтрокаНоменклатуры.Статус = 2 Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого Номенклатура1С ИЗ ПозицияНоменклатурыСбис.Номенклатура1С Цикл
					Если ЗначениеЗаполнено(Номенклатура1С.Ключ) Тогда
						СтрокаНоменклатуры.Статус = 0;
					Иначе
						СтрокаНоменклатуры.Статус = 1; 
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеНоменклатурДляРасширенногоСопоставления(ОбогащенныеДанныеНоменклатурыДляСопоставления = Неопределено)
	
	ПодготовленныеДанныеНоменклатур = Новый Массив;
	
	Если НЕ ОбогащенныеДанныеНоменклатурыДляСопоставления = Неопределено Тогда
		ДанныеНоменклатурДляРасширенногоСопоставления.Вставить("ПодготовленныеДанныеНоменклатур", ОбогащенныеДанныеНоменклатурыДляСопоставления);
		Возврат;
	КонецЕсли;
	
	ТабЧасть = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");
	Для Каждого ПозицияНоменклатурыКонтрагента Из ТабличнаяЧасть Цикл
		
		ГлавноеОкно = МестныйКэш.ГлавноеОкно;      
		ТекущийДокумент = сбисЭлементФормы(ЭтаФорма,"ТаблицаДокументов").ТекущиеДанные;
		ПутьТаблДок = ТекущийДокумент.ПутьТаблДок;
		ТекущееВложение = ТекущийДокумент.Вложение[0].Значение;
		ТаблДок = МестныйКэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ПутьТаблДок, ТекущееВложение.СтруктураФайла);  
		
		// Я так и не разобрался почему, но на некоторых вложениях структура файла пуста (спагетти код, но может потому что нет инишки нужной?), потому ставлю затычку
		Если ТаблДок = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИскомаяСтрокаТаблДок = Неопределено;
		
		Для Каждого СтрТабл Из ТаблДок Цикл
			
			Если СтрТабл.Название = ПозицияНоменклатурыКонтрагента.Название Тогда
				ИскомаяСтрокаТаблДок = СтрТабл;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ИскомаяСтрокаТаблДок = Неопределено Тогда
			
			ДанныеЗаполнения = Новый Структура();
			ДанныеЗаполнения.Вставить("КодПокупателя");
			ДанныеЗаполнения.Вставить("Код");
			ДанныеЗаполнения.Вставить("Артикул");
			ДанныеЗаполнения.Вставить("Название");
			ДанныеЗаполнения.Вставить("Идентификатор");
			ДанныеЗаполнения.Вставить("Номенклатура");
			ДанныеЗаполнения.Вставить("Характеристика");
			ДанныеЗаполнения.Вставить("GTIN");
			ДанныеЗаполнения.Вставить("КодПоставщика");
			ДанныеЗаполнения.Вставить("ЕдИзм");
			ДанныеЗаполнения.Вставить("ОКЕИ");
			ДанныеЗаполнения.Вставить("Цена");
			ДанныеЗаполнения.Вставить("Кол_Во");
			ДанныеЗаполнения.Вставить("Сумма");
			ДанныеЗаполнения.Вставить("СуммаБезНал");
			ДанныеЗаполнения.Вставить("ИдентификаторДокумента", СоставПакета.Идентификатор);
			
			ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ИскомаяСтрокаТаблДок);
			ПутьКонтрагента = "Файл.Документ.Отправитель";
			ГолыйКлассНоменклатурыДляСопоставления = МодульОбъектаКлиент().НовыйСтрокаСопоставленияСБИСКлиент(ДанныеЗаполнения);	
			
		КонецЕсли;
		
		СпособСопоставления = МестныйКэш.Парам.СпособСопоставленияНоменклатуры; 
		Если СпособСопоставления = 1 Тогда
			ИмяФормыРаботыСНоменклатурой = "СопоставлениеНоменклатуры_ДБФ"; 
		Иначе
			ИмяФормыРаботыСНоменклатурой = "СопоставлениеНоменклатуры_СБИС";
		КонецЕсли;                                                          
		
		ФормаРаботыСНоменклатурой = ГлавноеОкно.сбисНайтиФормуФункции("НоменклатураПоставщика_МассовыйПоиск", ИмяФормыРаботыСНоменклатурой,"", МестныйКэш);
		
		СписокНоменклатурДляОбогащения = Новый Массив; 
		СписокНоменклатурДляОбогащения.Добавить(ГолыйКлассНоменклатурыДляСопоставления);
		
		ПараметрыДляОбогащения = Новый Структура("Контрагент, Номенклатура");
		
		ПараметрыДляОбогащения.Вставить("Контрагент",	СоставПакета.Контрагент);  
		ПараметрыДляОбогащения.Вставить("Номенклатура",	СписокНоменклатурДляОбогащения);
		
		ДопПараметры = Новый Структура("Кэш", МестныйКэш);
		Попытка
			ОбогащенныеДанныеНоменклатурыДляСопоставления =	ФормаРаботыСНоменклатурой.НоменклатураПоставщика_МассовыйПоиск(ПараметрыДляОбогащения, ДопПараметры); 
		Исключение
			МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), "ФормаРаботыСНоменклатурой.НоменклатураПоставщика_МассовыйПоиск");
			Продолжить;
		КонецПопытки;
		
		Если МестныйКэш.Ини.Конфигурация.Свойство("Номенклатура") Тогда
			ИмяСправочникаНоменклатуры = СокрЛП(Сред(МестныйКэш.Ини.Конфигурация.Номенклатура.Значение, Найти(МестныйКэш.Ини.Конфигурация.Номенклатура.Значение, ".") + 1));
		Иначе
			ИмяСправочникаНоменклатуры = "Номенклатура";
		КонецЕсли;
		
		ПустаяСсылкаНоменклатуры = ПолучитьПустуюСсылкуСправочника(ИмяСправочникаНоменклатуры);
		Для Каждого ОбогащеннаяНоменклатура Из ОбогащенныеДанныеНоменклатурыДляСопоставления Цикл
			
			Если Не ОбогащеннаяНоменклатура.Номенклатура1С.Количество() Тогда 
				ГолыйКлассОписаниеНоменклатуры1С = МодульОбъектаКлиент().НовыйОписаниеНоменклатуры1СКлиент();
				ОбогащеннаяНоменклатура.Номенклатура1С.Вставить(ПустаяСсылкаНоменклатуры, ГолыйКлассОписаниеНоменклатуры1С);
			КонецЕсли;
			
			ОбогащеннаяНоменклатура.Вставить("НеобходимоСопоставление", Ложь);
			ПодготовленныеДанныеНоменклатур.Добавить(ОбогащеннаяНоменклатура);
			
		КонецЦикла; 
		
	КонецЦикла;
		
	ПорядокАвтоматическогоСопоставления = МодульОбъектаКлиент().ПолучитьЗначениеПараметраСБИС("ПорядокАвтоматическогоСопоставления");
	
	ИспользоватьАвтоматическоеСопоставление = МодульОбъектаКлиент().ПолучитьЗначениеПараметраСБИС("ИспользоватьАвтоматическоеСопоставлениеНоменклатуры");
	
	Если НЕ ИспользоватьАвтоматическоеСопоставление Тогда
		ДанныеНоменклатурДляРасширенногоСопоставления.Вставить("ПодготовленныеДанныеНоменклатур", ПодготовленныеДанныеНоменклатур);
		СоставПакета.Вставить("ДопПараметрыСопоставления", ДанныеНоменклатурДляРасширенногоСопоставления); 
		ЗаполнитьТаблицуДокументов(СоставПакета);
		
		Возврат;
	КонецЕсли;
	
	// TO DO Ду-ду Убрать как только появится список полей согласно инишкам (с) Сычев
	Если ПорядокАвтоматическогоСопоставления = Неопределено Тогда
		
		ПорядокАвтоматическогоСопоставления = Новый Массив;
		ПорядокАвтоматическогоСопоставления.Добавить("Артикул");
		ПорядокАвтоматическогоСопоставления.Добавить("КодПоставщика");
		ПорядокАвтоматическогоСопоставления.Добавить("КодПокупателя");
		ПорядокАвтоматическогоСопоставления.Добавить("GTIN");
		ПорядокАвтоматическогоСопоставления.Добавить("Идентификатор");
		ПорядокАвтоматическогоСопоставления.Добавить("Код");		
		
		АвтоматическоеСопоставлениеНоменклатур(ПодготовленныеДанныеНоменклатур, ПорядокАвтоматическогоСопоставления, ИмяСправочникаНоменклатуры);
		
	Иначе
		АвтоматическоеСопоставлениеНоменклатур(ПодготовленныеДанныеНоменклатур, ПорядокАвтоматическогоСопоставления, ИмяСправочникаНоменклатуры);
	КонецЕсли;
	
	ДанныеНоменклатурДляРасширенногоСопоставления.Вставить("ПодготовленныеДанныеНоменклатур", ПодготовленныеДанныеНоменклатур);
	
	СоставПакета.Вставить("ДопПараметрыСопоставления", ДанныеНоменклатурДляРасширенногоСопоставления); 
	ЗаполнитьТаблицуДокументов(СоставПакета);
	
КонецПроцедуры

&НаСервере
Процедура АвтоматическоеСопоставлениеНоменклатур(ПодготовленныеДанныеНоменклатур, ПорядокАвтоматическогоСопоставления, ИмяСправочникаНоменклатуры)
	
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.%СПРНоменклатура% КАК Номенклатура
	|ГДЕ
	|	Номенклатура.%ПараметрПоиска% = &ЗначениеПараметраПоиска";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%СПРНоменклатура%", ИмяСправочникаНоменклатуры);
	
	Для Каждого СтрокаНоменклатуры Из ПодготовленныеДанныеНоменклатур Цикл
		
		Для Каждого ПараметрПоиска Из ПорядокАвтоматическогоСопоставления Цикл
			
			ЗначениеПараметраПоиска = Неопределено; 
			
			// А вот чё тут делать? Не пришло поле какое-то, грустим
			Попытка
				Если НЕ СтрокаНоменклатуры.НоменклатураСБИС.Свойство(ПараметрПоиска) Тогда
					Продолжить;
				ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаНоменклатуры.НоменклатураСБИС[ПараметрПоиска]) Тогда
					Продолжить;
				КонецЕсли;
			Исключение
				Продолжить;
			КонецПопытки;
			
			ЗначениеПараметраПоиска = СтрокаНоменклатуры.НоменклатураСБИС[ПараметрПоиска];
			Запрос.УстановитьПараметр("ЗначениеПараметраПоиска", ЗначениеПараметраПоиска);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПараметрПоиска%", ПараметрПоиска); 
			НайденнаяНоменклатура = Запрос.Выполнить().Выбрать();
			
			Если НайденнаяНоменклатура.Количество() Тогда
				
				Для Каждого Номенклатура1С Из НайденнаяНоменклатура Цикл          
					ОписаниеНоменклатуры1С = МодульОбъектаСервер().НовыйОписаниеНоменклатуры1ССервер();
					СтрокаНоменклатуры.Номенклатура1С.Вставить(Номенклатура1С.Ссылка, ОписаниеНоменклатуры1С); 
				КонецЦикла;  
				
				Прервать; 
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьБезопасноеНаименование(ИсходнаяСтрока) Экспорт
	
	Результат = "";
	
	Латиница = "QWERTYUIOPASDFGHJKLZXCVBNMqwertyuiopasdfghjklzxcvbnm";
	Кириллица = "абвгдеёзжийклмнопрстуфхцчшщъыьэюяАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ";
	Цифры = "0123456789";                                                            
	ДопустимыеСимволы = Латиница + Кириллица + Цифры;
	
	Для ПозицияСимвола = 1 по СтрДлина(ИсходнаяСтрока) Цикл
		ТекСимв = Сред(ИсходнаяСтрока, ПозицияСимвола, 1);
		Если Найти(ДопустимыеСимволы, ТекСимв) > 0 Тогда
			Результат = Результат + ТекСимв;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВРЕГ(Результат);
	
КонецФункции

&НаСервере
Функция ПолучитьПустуюСсылкуСправочника(ИмяСправочника)
	
	ИмяПредопределенногоЭлемента = "Справочник." + ИмяСправочника + ".ПустаяСсылка";
	Возврат ПредопределенноеЗначение(ИмяПредопределенногоЭлемента);	
	
КонецФункции
#КонецОбласти

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_РасширенноеСопоставление_ОбработчикиСобытий

&НаКлиенте
Процедура ОткрытьФормуСопоставления(Элемент) Экспорт    
	
	ДанныеНоменклатурыЗаполнены = Ложь;
	ТабЧасть = сбисЭлементФормы(ЭтаФорма,"ТабличнаяЧасть");
	
	Для Каждого ПозицияНоменклатурыКонтрагента Из ДанныеНоменклатурДляРасширенногоСопоставления.ПодготовленныеДанныеНоменклатур Цикл
		Если ПозицияНоменклатурыКонтрагента.НоменклатураСБИС.Наименование = ТабЧасть.ТекущиеДанные.Название Тогда
			ДанныеНоменклатурыЗаполнены = Истина;
			ИскомаяНоменклатура = ПозицияНоменклатурыКонтрагента; 
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ДанныеНоменклатурыЗаполнены Тогда
		// TO DO Ду-ду обработать ошибку для пользователя
		Возврат;
	КонецЕсли;
	
	ИскомаяНоменклатура.НоменклатураСБИС.Вставить("Количество", ТабЧасть.ТекущиеДанные.Количество);
	ИскомаяНоменклатура.НоменклатураСБИС.Вставить("Цена", ТабЧасть.ТекущиеДанные.Цена);  
	ИскомаяНоменклатура.НоменклатураСБИС.Вставить("СтавкаНДС", ТабЧасть.ТекущиеДанные.СтавкаНДС);
	ИскомаяНоменклатура.НоменклатураСБИС.Вставить("СуммаНДС", ТабЧасть.ТекущиеДанные.СуммаНДС);
	ИскомаяНоменклатура.НоменклатураСБИС.Вставить("СуммаБезНДС", ТабЧасть.ТекущиеДанные.СуммаБезНал);
	ИскомаяНоменклатура.НоменклатураСБИС.Вставить("Сумма", ТабЧасть.ТекущиеДанные.Сумма);
	
	фрм = МестныйКэш.ГлавноеОкно.сбисПолучитьФорму("ФормаРучногоСопоставленияКонтрагентов");   
	ЗаголовокФормыРучногоСопоставления = "Сопоставления по номенклатуре " +"'" + Табчасть.ТекущиеДанные.Название + "'";

	ДопПараметры = Новый Структура;
	
    фрм.ОписаниеОповещенияОЗакрытии = МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("ПослеРучнойОбработкиСопоставления", ЭтаФорма, ДопПараметры);
	фрм.Показать(Новый Структура("ОсновныеДанные, Кэш, Заголовок", ИскомаяНоменклатура, МестныйКэш, ЗаголовокФормыРучногоСопоставления));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуруВ1С(Команда)
    МестныйКэш.Вставить("РезультатДействия", МестныйКэш.ОбщиеФункции.РезультатДействия_Получить(МестныйКэш,Новый Структура("ПредставлениеОперации", "СозданиеНоменклатуры1С"),Истина));
	ЗагрузитьВложения(1);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСопоставлениеНоменклатуры(Команда)
	
	НеВсяНоменклатураСопоставлена = Ложь;
	Для Каждого НоменклатураСБИС Из ТабличнаяЧасть Цикл
		Если НоменклатураСбис.Статус = 1 Тогда
			НеВсяНоменклатураСопоставлена = Истина;
			Сообщить("Перед сохранением требуется сопоставить всю номенклатуру");
			Возврат;	
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыДляОбновления = Новый Структура("Контрагент, Номенклатура");
	
	ПараметрыДляОбновления.Вставить("Контрагент", СоставПакета.Контрагент);  
	ПараметрыДляОбновления.Вставить("Номенклатура", ДанныеНоменклатурДляРасширенногоСопоставления.ПодготовленныеДанныеНоменклатур);
	
	фрм = МестныйКэш.ГлавноеОкно.сбисНайтиФормуФункции("НоменклатураПоставщика_МассовоеОбновление",МестныйКэш.ФормаРаботыСНоменклатурой,"", МестныйКэш);
	ДопПараметры = Новый Структура("Кэш, Идентификатор", МестныйКэш, СоставПакета.Идентификатор);
	фрм.НоменклатураПоставщика_МассовоеОбновление(ПараметрыДляОбновления, ДопПараметры);
	
	Для Каждого НоменклатураДокумента Из ТабличнаяЧасть Цикл
		НоменклатураДокумента.Статус = 0;
	КонецЦикла;
	ОбновитьСтатусыСопоставления();
		
	Сообщить("Номенклатура успешно сопоставлена"); 
	
КонецПроцедуры

#КонецОбласти  

// alo EDI_ДозаписьЮЗДО >>
МассивЗакладок = Новый Массив;
МассивЗакладок.Добавить("Документы");
МассивЗакладок.Добавить("Загрузка");
МассивЗакладок.Добавить("Прохождение");  
МассивЗакладок.Добавить("Маркировка"); 
МассивЗакладок.Добавить("Прослеживаемость");

